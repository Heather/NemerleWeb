using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Imperative;
using Nemerle.Macros;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using PT = Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Assertions;
using System.IO;
using System.Text.RegularExpressions;
using TSParser;
using TSParser.Ast;
using NemerleWeb;

namespace NemerleWeb
{
  /// <summary>
  /// Description of TSGenerator.
  /// </summary>
  internal module TSGenerator
  {
    #region Public
    
    /// <summary>
    /// Create namespace and module 
    /// </summary>
    public GenerateCode(
      [NotNull] typer : Typer,
      [NotNull] root : string,
      [NotNull] tsAst : IEnumerable[DeclarationElement]) : void
    {
      def jsApiRoot = typer.Env.EnterIntoNamespace(root.SplitDot());
      
      // Predefined types
      GeneratePredefined(jsApiRoot);
      
      // First generate all types
      GenerateTypes(jsApiRoot, tsAst);
      
      // Generate types members
      GenerateTypesMembers(jsApiRoot, tsAst);
      
      // Then generate the rest: variables, functions...
      GenerateRest(jsApiRoot, tsAst);
    }
    
    /// <summary>
    /// Define some predefined types:
    /// * $Unit - a simple type for faking void types in generics.
    /// </summary>
    private GeneratePredefined(typesScope : GlobalEnv) : void
    {
      typesScope.Define(<[ decl: [JSApi] public class $(UnitClass : usesite) {} ]>).Compile();
    }
    
    #endregion
    
    #region Generate types
    
    private GenerateTypes(typesScope : GlobalEnv, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      {
      | InterfaceDeclaration(value) => CreateInterface(typesScope, value, false)
      | AmbientDeclaration(value) => GenerateTypesAmbient(typesScope, value)
      | _ => ()
      }
    }
    
    private GenerateTypesAmbient(typesScope : GlobalEnv, ambient : AmbientDeclaration) : void
    {
      match(ambient)
      {
      | VariableDeclaration
      | FunctionDeclaration => ()
      | ClassDeclaration(value) => CreateClass(typesScope, value, false)
      | EnumDeclaration(value) => CreateEnum(typesScope, value)
      | ExternalModuleDeclaration => ()
      | ModuleDeclaration(value) => GenerateTypesModule(typesScope, value)
      | _ => ()
      }
    }
    
    private GenerateTypesModule(typesScope : GlobalEnv, @module : AmbientModuleDeclaration) : void
    {
      def moduleScope = typesScope.EnterIntoNamespace(@module.Name.SplitDot());
      
      foreach (member in @module.Members)
      {
        | VariableDeclaration
        | FunctionDeclaration => ()
        | ClassDeclaration(value) => CreateClass(moduleScope, value, false)
        | InterfaceDeclaration(value) => CreateInterface(moduleScope, value, false)
        | EnumDeclaration(value) => CreateEnum(moduleScope, value)
        | ModuleDeclaration(value) => GenerateTypesModule(moduleScope, value)
        | _ => ()
      }
    }
    
    #endregion
    
    #region Generate types members
    
    private GenerateTypesMembers(typesScope : GlobalEnv, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      { 
      | InterfaceDeclaration(value) => CreateInterface(typesScope, value, true)
      | AmbientDeclaration(value) => GenerateTypesMembersAmbient(typesScope, value)
      | _ => ()
      }
    }
    
    private GenerateTypesMembersAmbient(typesScope : GlobalEnv, ambient : AmbientDeclaration) : void
    {
      match(ambient)
      {
      | VariableDeclaration
      | FunctionDeclaration => ()
      | ClassDeclaration(value) => CreateClass(typesScope, value, true)
      | EnumDeclaration          
      | ExternalModuleDeclaration => ()
      | ModuleDeclaration(value) => GenerateTypesMembersModule(typesScope, value)
      | _ => ()
      }
    }
     
    private GenerateTypesMembersModule(typesScope : GlobalEnv, @module : AmbientModuleDeclaration) : void
    {
      def moduleScope = typesScope.EnterIntoNamespace(@module.Name.SplitDot());
      
      foreach (member in @module.Members)
      {
        | VariableDeclaration
        | FunctionDeclaration => ()
        | ClassDeclaration(value) => CreateClass(moduleScope, value, true)
        | InterfaceDeclaration(value) => CreateInterface(moduleScope, value, true)
        | EnumDeclaration => ()
        | ModuleDeclaration(value) => GenerateTypesMembersModule(moduleScope, value)
        | _ => ()
      }
    }
    
    #endregion
    
    #region Generate rest
    
    private GenerateRest(typesScope : GlobalEnv, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      { 
      | AmbientDeclaration(value) => GenerateRestAmbient(typesScope, value)
      | _ => ()
      }
    }
    
    private GenerateRestAmbient(typesScope : GlobalEnv, ambient : AmbientDeclaration) : void
    {
      // Create module
      def membersScope = GetTypeBuilder(typesScope);
      
      match(ambient)
      {
      | VariableDeclaration(value) => CreateVariable(membersScope, value)
      | FunctionDeclaration(value) => CreateFunction(membersScope, value)
      | ExternalModuleDeclaration => ()
      | ModuleDeclaration(value) => GenerateRestModule(typesScope, value)
      | _ => ()
      }
      
      // Compile module
      membersScope.Compile();
    }
    
    private GenerateRestModule(typesScope : GlobalEnv, @module : AmbientModuleDeclaration) : void
    {
      def moduleScope = typesScope.EnterIntoNamespace(@module.Name.SplitDot());
      
      // Create module
      def membersScope = GetTypeBuilder(moduleScope);
      
      foreach (member in @module.Members)
      {
        | VariableDeclaration(value) => CreateVariable(membersScope, value)
        | FunctionDeclaration(value) => CreateFunction(membersScope, value)
        | ClassDeclaration
        | InterfaceDeclaration
        | EnumDeclaration => ()
        | ModuleDeclaration(value) => GenerateRestModule(moduleScope, value)
        | _ => ()
      }
      
      membersScope.Compile();
    }
    
    #endregion
    
    #region Code Generation
    
    private CreateInterface(
      typesScope : GlobalEnv,
      tsInterface : InterfaceDeclaration,
      generateMembers : bool) : void
    {
      def typeParameters = ConvertTypeParameters(tsInterface.TypeParameters);
      
      def extends = 
        if (generateMembers)
          tsInterface.Extends.Select(e => MakeFullType(typesScope, e));
        else
          [];
        
      def interfaceType = typesScope.Define(
        <[ decl:
          [JSApi]
          public partial interface $(tsInterface.Name : usesite)[..$typeParameters] : ..$extends {}
        ]>);
      
      when (generateMembers)
        CreateObject(interfaceType, tsInterface.Type, tsInterface.Name);
        
      interfaceType.Compile();
    }

    private CreateVariable(membersScope : TypeBuilder, var : AmbientVariableDeclaration) : void
    {
      def typePrefix = string.Join(Separator, var.Name, Variable);
      def varType = ConvertType(membersScope, var.Type, typePrefix);
      
      _ = membersScope.Define(
        <[ decl: public new mutable $(var.Name : usesite) : $varType = default($varType); ]>);
    }
  
    private CreateFunction(membersScope : TypeBuilder, function : AmbientFunctionDeclaration) : void
    {
      CreateMethod(membersScope, function.Name, function.Signature, "");
    }
    
    CreateObject(membersScope : TypeBuilder, obj : ScriptType.Object, prefix : string) : void
    {
      foreach(mem in obj.Members) CreateObject(membersScope, mem, prefix);
    }
    
    CreateObject(membersScope : TypeBuilder, obj : ScriptObject, prefix : string) : void
    {
      match(obj)
      {
      | Call(value)      => CreateCall     (membersScope, value, prefix)
      | Construct(value) => CreateConstruct(membersScope, value, prefix)
      | Index(value)     => CreateIndex    (membersScope, value, prefix)
      | Property(value)  => CreateProperty (membersScope, value, prefix)
      | Method(value)    => CreateMethod   (membersScope, value, prefix)
      | _ => ()
      }
    }
    
    CreateCall(membersScope : TypeBuilder, call : CallSignature, prefix : string) : void
    {
      def fullPrefix = string.Join(Separator, prefix, Call);
      def returnTypePrefix = string.Join(Separator, fullPrefix, Return);
      def realReturnType = ConvertType(membersScope, call.ReturnType, returnTypePrefix);
      
      def functionParams = ConvertParameters(membersScope, call.Parameters, fullPrefix);
      when(functionParams is None()) return;      
      def functionParams = functionParams.Value;
      
      def typeParameters = ConvertTypeParameters(call.TypeParameters);
      
      membersScope.DefineMemberPublicNew(
        <[ decl: $(JSApiSpecial.Call : usesite)[..$typeParameters](..$functionParams) : $realReturnType; ]>);
    }
   
    CreateConstruct(membersScope : TypeBuilder, construct : Construct, prefix : string) : void
    {
      def fullPrefix = string.Join(Separator, prefix, New);
      def returnTypePrefix = string.Join(Separator, fullPrefix, Return);
      def realReturnType = ConvertType(membersScope, construct.ReturnType, returnTypePrefix);
    
      def functionParams = ConvertParameters(membersScope, construct.Parameters, fullPrefix);
      when(functionParams is None()) return;
      def functionParams = functionParams.Value;
      
      def typeParameters = ConvertTypeParameters(construct.TypeParameters);
      
      membersScope.DefineMemberPublicNew(
        <[ decl: $(JSApiSpecial.New : usesite)[..$typeParameters](..$functionParams) : $realReturnType; ]>);
    }
    
    CreateIndex(membersScope : TypeBuilder, index : IndexSignature, prefix : string) : void
    {
      def (realParamType, typeName) =
        match(index.ParameterType)
        {
        | Number => (<[ float ]>, "Number")
        | String => (<[ string ]>, "String")
        };
        
      def fullPrefix = string.Join(Separator, prefix, typeName, Index);
      def returnTypePrefix = string.Join(Separator, fullPrefix, Return);
      def realReturnType = ConvertType(membersScope, index.ReturnType, returnTypePrefix);
      
      membersScope.DefineMemberPublicNew(
        <[ decl:
          new Item[$(index.ParameterName : usesite) : $realParamType] : $realReturnType
          {
            get;
            set;
          }
        ]>);
    }

    /// <summary>
    /// Don't pay attention to optional, always generate property
    /// </summary>
    CreateProperty(membersScope : TypeBuilder, property : Property, prefix : string) : void
    {
      def fullPrefix = string.Join(Separator, prefix, property.Name);
      def typePrefix = string.Join(Separator, fullPrefix, Property);
      def propertyType = ConvertType(membersScope, property.Type, typePrefix);
      
      membersScope.DefineMemberPublicNew(
        <[ decl: $(property.Name : usesite) : $propertyType { get; set; }; ]>);
    }
    
    /// <summary>
    /// Don't pay attention to optional, always generate method
    /// </summary>
    CreateMethod(membersScope : TypeBuilder, method : Method, prefix : string) : void
    {
      CreateMethod(membersScope, method.Name, method.Signature, prefix);
    }
    
    CreateMethod(
      membersScope : TypeBuilder,
      name : string,
      signature : CallSignature,
      prefix : string) : void
    {
      def fullPrefix = string.Join(Separator, prefix, name);
      def returnTypePrefix = string.Join(Separator, fullPrefix, Return);
      def realReturnType = ConvertType(membersScope, signature.ReturnType, returnTypePrefix);
    
      def functionParams = ConvertParameters(membersScope, signature.Parameters, fullPrefix);
      when(functionParams is None()) return;
      def functionParams = functionParams.Value;
      
      def typeParameters = ConvertTypeParameters(signature.TypeParameters);
    
      membersScope.DefineMemberPublicNew(
        <[ decl: $(name : usesite)[..$typeParameters](..$functionParams) : $realReturnType; ]>);
    }
    
    // TODO: Remove
    static mutable anonymousTypeIndex : int = 0;
    
    /// <summary>
    /// Anonymous type is always created as nested type to properly handle generics.
    /// </summary>
    private CreateAnonymousType(
      membersScope : TypeBuilder,
      obj : ScriptType.Object,
      prefix : string) : PExpr
    {
      // Filter out pseudo types
      if(obj.Members is [])
        <[ object ]>
      else
      {
        anonymousTypeIndex++;
        
        def name = string.Join(Separator, prefix, AnonymousType, anonymousTypeIndex.ToString());
        
        def type = membersScope.DefineNestedTypeWithSource(
          <[ decl: 
            [JSApi] public partial class $(name : usesite) { }
          ]>);
          
        // Suppress warning
        type.HasBeenUsed = true;
        
        CreateObject(type, obj, name);
        type.Compile();
          
        <[ $(name : usesite) ]>
      }
    }
    
    private CreateEnum(typesScope : GlobalEnv, tsEnum : AmbientEnumDeclaration) : void
    {
      def members = tsEnum.Members
        .Select(member => 
          {
            if(member.Value is Some(enumValue))
              <[ decl: | $(member.Name : usesite) = $enumValue ]>
            else
              <[ decl: | $(member.Name : usesite) ]>
          });
      
      typesScope.Define(<[ decl: public enum $(tsEnum.Name : usesite) { ..$members } ]>).Compile();
    }
    
    private CreateClass(
      typesScope : GlobalEnv,
      tsClass : AmbientClassDeclaration,
      generateMembers : bool) : void
    {
      def typeParameters = ConvertTypeParameters(tsClass.TypeParameters);
        
      def extends = 
        if (generateMembers)
        {
          def baseClass = if(tsClass.Heritage.Base is Some(baseClass)) [baseClass] else [];
          baseClass.Concat(tsClass.Heritage.Interfaces).Select(e => MakeFullType(typesScope, e));
        }
        else
          [];
      
      def classType = typesScope.Define(
        <[ decl:
          [JSApi]
          public partial interface $(tsClass.Name : usesite)[..$typeParameters] : ..$extends {}
        ]>);
      
      when (generateMembers)
        CreateClassMembers(typesScope, classType, tsClass, tsClass.Name);
        
      classType.Compile();
    }
    
    private CreateClassMembers(
      typesScope : GlobalEnv,
      membersScope : TypeBuilder,
      tsClass : AmbientClassDeclaration,
      prefix : string) : void
    {
    }
    
    #endregion
    
    #region Converters
    
    /// <summary>
    /// Makes type with type argument
    /// </summary>
    private MakeFullType(typesScope : GlobalEnv, typeRef : ScriptType.Ref) : PExpr
    {
      def typeArguments = typeRef.TypeArguments.Select(
        tyArg => MakeFullTypeImpl(typesScope, tyArg));
      
      def realType = PExprHelper.FromQualifiedIdentifierNonHygienic(typeRef.Name);
        
      <[ $realType.[..$typeArguments] ]>
    }
    
    /// <summary>
    /// Get PExpr from TS type
    /// </summary>
    private MakeFullTypeImpl(typesScope : GlobalEnv, type : ScriptType) : PExpr
    {
      match(type)
      {
      | Any => <[ object ]>
      | Number => <[ float ]>
      | Boolean => <[ bool ]>
      | String => <[ string ]>
      | Void => <[ void ]>
      | Ref as r => MakeFullType(typesScope, r)
      | Query => Message.Warning("Query type argument unsupported"); <[ object ]>
      | Array => Message.Warning("Array type argument unsupported"); <[ object ]>
      | Object as o =>
        if(o.Members is [])
          <[ object ]>
        else
        {
          Message.Warning("Object type argument is supported only without members");
          <[ object ]>
        }        
      | Function => Message.Warning("Function type argument unsupported"); <[ object ]>
      | Constructor => Message.Warning("Constructor type argument unsupported"); <[ object ]>
      | _ => Message.Warning($"Unsupport type argument $type");  <[ object ]>
      }
    }
    
    /// <summary>
    /// Type parameters are used in the type itself.
    /// </summary>
    private ConvertTypeParameters(typeParameters : IEnumerable[ScriptTypeParameter])
      : list[PT.Splicable]
    {
      def convert(p : ScriptTypeParameter) : PT.Splicable
      {
        def realName = if(p.Name != "void") p.Name else UnitClass;
        PT.Splicable.Name(<[ $(Name(realName) : name) ]>)  
      }
      
      typeParameters.Select(p => convert(p)).NToList()
    }
    
    private GetParameterPrefix(prefix : string, name : string) : string
    {
      string.Join(Separator, prefix, name, Parameter)
    }
    
    /// <summary>
    /// Convert function parameters
    /// If there is a literal parameter, None is returned.
    /// </summary>
    private ConvertParameters(
      membersScope : TypeBuilder,
      parms : IEnumerable[ScriptParameter],
      prefix : string) : option[List[PParameter]]
    {
      def convert(p : ScriptParameter) : option[PParameter]
      {
        // Literal parameters are not supported now
        | Required(RequiredParameter.Literal) => None()          
        | Required(RequiredParameter.Regular(name, type, _))
        | Optional(name, type, _, _) => 
          def realType = ConvertType(membersScope, type, GetParameterPrefix(prefix, name));
          Some(<[ parameter: $(name : usesite) : $realType ]>)
        | Rest(name, type) =>          
          def realType = ConvertType(membersScope, type, GetParameterPrefix(prefix, name));
          Some(<[ parameter: params $(name : usesite) : $realType ]>)
      }
      
      def ret = List();
      foreach(parm in parms)
      {
        def maybeRealParam = convert(parm);
        if(maybeRealParam is Some(realParam))
          ret.Add(realParam);
        else
          return None();
      }
      
      Some(ret);
    }
    
    /// <summary>
    /// Return parameters types
    /// </summary>
    private GetParametersType(membersScope : TypeBuilder, parms : list[ScriptParameter], prefix : string) : list[PExpr]
    {
      def convertParamsType(p : ScriptParameter)
      {
        // Literal parameters are not supported now
        | Required(RequiredParameter.Literal) =>
          throw InvalidOperationException("Literal parameters are not supported")
        | Required(RequiredParameter.Regular(name, type, _))
        | Optional(name, type, _, _) => 
          ConvertType(membersScope, type, GetParameterPrefix(prefix, name))
        | Rest(name, type) =>
          ConvertType(membersScope, type, GetParameterPrefix(prefix, name))
      }
    
      def ret = parms.Map(convertParamsType);
      ret
    }
    
    private ConvertType(membersScope : TypeBuilder, type : option[ScriptType], prefix : string) : PExpr
    {
      if(type is Some(t)) ConvertType(membersScope, t, prefix) else <[ object ]>
    }
    
    private ConvertType(membersScope : TypeBuilder, type : ScriptType, prefix : string) : PExpr
    {
      match(type)
      {
      | Any => <[ object ]>
      | Array(t, 0) => <[ $(ConvertType(membersScope, t, prefix)) ]>
      | Array(t, rank) => <[ array[$(ConvertType(membersScope, ScriptType.Array(t, rank - 1), prefix))] ]>
      | Boolean => <[ bool ]>
      | Constructor => <[ object ]>
      | Function(value) =>
        // TODO: Handle generic function literals
        if(value.TypeParameters is [])
        {
          mutable lambdaParams = GetParametersType(membersScope, value.Parameters, prefix);
          def type =
            if(value.ReturnType is ScriptType.Void())
              <[ System.Action ]>
            else
            {
              lambdaParams = lambdaParams.Concat(
                [ConvertType(membersScope, value.ReturnType, prefix)]);
              <[ System.Func ]>
            };
          
          <[ $type.[..$lambdaParams] ]>
        }
        else
        {
          <[object]>
        }
        
      | Number => <[ float ]>
      | Object as type => CreateAnonymousType(membersScope, type, prefix)
      | Query as q =>
        // TODO: Support query
        Message.Hint($"Query is not supported, typeof $(q.Name)");
        <[ object ]>      
      | Ref("Array", tyArgs) =>
        // TODO: Support local Array type
        def tyArg =
          match(tyArgs)
          {
          | [] => ScriptType.Any()
          | [tyArg] => tyArg
          | _ =>
            Message.Warning(@"Array generic type cannot receive more than one type argument,"
                            @"using the first one.");
            tyArgs.Head
          };
          
        def typeArgumentPrefix = string.Join(Separator, prefix, TypeArgument);
        def realType = ConvertType(membersScope, tyArg, typeArgumentPrefix);
        
        <[ array[$realType] ]>
          
      | Ref(name, tyArgs) => 
        def typeArgumentPrefix = string.Join(Separator, prefix, TypeArgument);
        def realTypeArguments = ConvertTypeArguments(membersScope, tyArgs, typeArgumentPrefix);
        def realType = PExprHelper.FromQualifiedIdentifierNonHygienic(name);
        
        <[ $realType.[..$realTypeArguments] ]>
      | String => <[ string ]>
      | Void => <[ void ]>
      }
    }
    
    private ConvertTypeArguments(
      membersScope : TypeBuilder,
      tyArgs : IEnumerable[ScriptType],
      prefix : string) : list[PExpr]
    {
      def convert(typeArg : ScriptType) : PExpr
      {
        if(typeArg is ScriptType.Void())
          <[ $(Name(UnitClass) : name) ]>
        else
          ConvertType(membersScope, typeArg, prefix);
      }
      
      tyArgs.Select(typeArg => convert(typeArg)).NToList()
    }
    
    #endregion
    
    #region Special constants
    
    private Call = "Call";
    private New = "New";
    private Variable = "Variable";
    private Constructor = "Constructor";
    private Index = "Index";
    private Return = "Return";
    private Parameter = "Parameter";
    private AnonymousType = "AnonymousType";
    private TypeArgument = "TypeArgument";
    private Separator = "_";
    
    private UnitClass = "$Unit";
    
    #endregion
    
    #region Helpers
    
    public GetTypeBuilder(globalEnv : GlobalEnv) : TypeBuilder
    { 
      def moduleName = globalEnv.CurrentNamespace.PartName;
      def parent = globalEnv.Manager.CoreEnv.EnterIntoNamespace(globalEnv.CurrentNamespace.Parent);
      
      parent.Define(<[ decl: public partial module $(moduleName : usesite) {} ]>)
    }
    
    #endregion
  }
  
  internal module ScopeExtensions
  {
    public DefineMemberPublicNew(this scope : TypeBuilder, member : ClassMember) : void
    {
      when(!scope.IsInterface)
      {
        match(member)
        {
        | f is ClassMember.Function =>
          f.body = DefaultFunctionImplementation;
        | p is ClassMember.Property =>
          when(p.getter is Some(getter))
            getter.body = DefaultFunctionImplementation;
          when(p.setter is Some(setter))
            setter.body = DefaultFunctionImplementation;
        | _ => ()
        }
      }
      
      member.modifiers.mods |= NemerleModifiers.New;
      when(!scope.IsInterface)
        member.modifiers.mods |= NemerleModifiers.Public;
      
      _ = scope.DefineWithSource(member);
    }
    
    private DefaultImplementation : PExpr =
      <[ { IgnoreParams(); throw NemerleWeb.ClientCodeCalledInServerException(); } ]>;
    private DefaultFunctionImplementation : FunBody.Parsed =
      FunBody.Parsed(DefaultImplementation);
  }

  internal module PExprHelper
  {
    /// <summary>
    /// Transforms dot-separated identifier to the parse-tree expression
    /// Unlike PExpr.FromQualifiedIdentifier, this method breaks higiene.
    /// Use with caution.
    /// </summary>
    public static FromQualifiedIdentifierNonHygienic(qid : string) : PExpr
    {
      if (string.IsNullOrEmpty(qid)) null
      else
      {
        def split = qid.Split('.');
        mutable expr = <[ $(Name(split[0]) : name) ]>;
        for (mutable i = 1; i < split.Length; i++)
          expr = <[ $expr . $(Name(split[i]) : name) ]>;
        expr
      }
    }
  }
}
