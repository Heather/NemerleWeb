using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Imperative;
using Nemerle.Macros;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using PT = Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Assertions;
using System.IO;
using System.Text.RegularExpressions;
using TSParser;
using TSParser.Ast;
using NemerleWeb;

namespace NemerleWeb
{
  /// <summary>
  /// Description of TSGenerator.
  /// </summary>
  internal module TSGenerator
  {
    #region Public
    
    public GenerateCode(typer : Typer, root : string, tsAst : IEnumerable[DeclarationElement]) : void
    {
      def jsApiNamespace = typer.Env.EnterIntoNamespace(root.SplitDot());
      //def jsApiModule = CreateModule(root.SplitDot(), Scope(typer.Env), [], true, true);
      
      // First generate all types
      GenerateTypes(Scope(jsApiNamespace), tsAst);
            
      // Generate types members
      //GenerateTypesMembers(Scope(jsApiNamespace), tsAst);
      
      // Then generate the rest: variables, functions.
      //GenerateRestMembers(Scope(jsApiNamespace), jsApiModule, tsAst);
      
      // Finalize
      //jsApiModule.Compile();
    }
    
    #endregion
    
    #region Generate types
    
    private GenerateTypes(scope : Scope, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      { 
      | InterfaceDeclaration(value) => CreateInterface(scope, value)
      | AmbientDeclaration(value) => GenerateTypesAmbient(scope, value)
      | _ => ()
      }
    }
    
    private GenerateTypesAmbient(scope : Scope, ambient : AmbientDeclaration) : void
    {
      match(ambient)
      {
      | VariableDeclaration      
      | FunctionDeclaration      
      | ClassDeclaration         
      | EnumDeclaration          
      | ExternalModuleDeclaration => ()
      | ModuleDeclaration(value) => ()
      | _ => ()
      }
    }
    
    private GenerateTypesModule() : void
    {
    }
    
    #endregion
    
    #region Generate types members
    
    private GenerateTypesMembers(scope : Scope, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      { 
      | InterfaceDeclaration(value) => CreateInterfaceMembers(scope, value)
      //| AmbientDeclaration(value) => GenerateTypesAmbient(scope, value)
      | _ => ()
      }
    }
    
    #endregion
    
    #region Code Generation
    
    private CreateInterface(
      scope : Scope,
      tsInterface : InterfaceDeclaration) : void
    {
      def typeParameters = tsInterface.TypeParameters
        .Select(e => PT.Splicable.Name(PT.Name(e.Name)))
        .NToList();
        
      def extends = tsInterface.Extends
        .Select(e => ConvertTypeArgument(scope, e, typeParameters));
        
      def interfaceType = scope.Define(
        <[ decl:
          [JSApi]
          public partial interface $(tsInterface.Name : usesite)[..$typeParameters] : ..$extends {}
        ]>);
      
      interfaceType.Compile();
    }
    
    private CreateInterfaceMembers(
      scope : Scope,
      tsInterface : InterfaceDeclaration) : void
    {
      def typeParameters = tsInterface.TypeParameters
        .Select(e => PT.Splicable.Name(PT.Name(e.Name)))
        .NToList();
        
      def interfaceType = scope.Define(
        <[ decl:
          [JSApi]
          public partial interface $(tsInterface.Name : usesite)[..$typeParameters] {}
        ]>);
        
      CreateObject(interfaceType, tsInterface.Type, tsInterface.Name);
      
      interfaceType.Compile();
    }
    
    CreateObject(scope : TypeBuilder, obj : ScriptType.Object, prefix : string) : void
    {
      foreach(mem in obj.Members) CreateObject(scope, mem, prefix);
    }
    
    CreateObject(scope : TypeBuilder, obj : ScriptObject, prefix : string) : void
    {
      match(obj)
      {
      | Call(value)      => CreateCall(scope, value, prefix)
      | Construct(value) => CreateConstruct(scope, value, prefix)
      | Index(value)     => CreateIndex(scope, value, prefix)
      | Property(value)  => CreateProperty(scope, value, prefix)
      | Method(value)    => CreateMethod(scope, value, prefix)
      | _ => ()
      }
    }
    
    CreateCall(scope : TypeBuilder, call : CallSignature, prefix : string) : void
    {
      def fullPrefix = string.Join(prefix, Call);
      def returnTypePrefix = string.Join(fullPrefix, Return);
      def realReturnType = <[ object ]>;//ConvertType(returnTypePrefix, returnType, scope);
      
      def functionParams = ConvertParameters(scope, call.Parameters, fullPrefix);
      
      _ = scope.DefineWithSource(
        if(scope.IsInterface)
          <[ decl: new $(JSApiSpecial.Call : usesite)(..$functionParams) : $realReturnType; ]>
        else
          <[ decl:
             public $(JSApiSpecial.Call : usesite)(..$functionParams) : $realReturnType
              $DefaultImplementation
          ]>);
    }
   
    CreateConstruct(scope : TypeBuilder, construct : Construct, prefix : string) : void
    {
      def fullPrefix = string.Join(prefix, New);
      def returnTypePrefix = string.Join(fullPrefix, Return);
      def realReturnType = <[ object ]>;;//ConvertType(returnTypePrefix, type, scope);
    
      def functionParams = ConvertParameters(scope, construct.Parameters, fullPrefix);
      
      _ = scope.DefineWithSource(
          if(scope.IsInterface)
            <[ decl: new $(JSApiSpecial.New : usesite)(..$functionParams) : $realReturnType; ]>
          else
            <[ decl:
              public $(JSApiSpecial.New : usesite)(..$functionParams) : $realReturnType
                $DefaultImplementation
            ]>);
    }
    
    CreateIndex(scope : TypeBuilder, index : IndexSignature , prefix : string) : void
    {
      def (realParamType, typeName) =
        match(index.ParameterType)
        {
        | Number => (<[ float ]>, "Number")
        | String => (<[ string ]>, "String")
        };
        
      def fullPrefix = string.Join(prefix, typeName, Index);
      def returnTypePrefix = string.Join(fullPrefix, Return);
      def realReturnType = <[ object ]>;//ConvertType(returnTypePrefix, returnType, scope);
      
      _ = scope.DefineWithSource(
        if(scope.IsInterface)
          <[ decl:
            new Item[$(index.ParameterName : usesite) : $realParamType] : $realReturnType
            {
              get;
              set;
            }
          ]>
        else
          <[ decl:
            public Item[$(index.ParameterName : usesite) : $realParamType] : $realReturnType
            {
              get
                $DefaultImplementation;
              set
                $DefaultImplementation;
            }
          ]>);
    }

    /// <summary>
    /// Don't pay attention to optional, always generate property
    /// </summary>
    CreateProperty(scope : TypeBuilder, property : Property, prefix : string) : void
    {
      def fullPrefix = string.Join(prefix, property.Name);
      def typePrefix = string.Join(fullPrefix, Property);
      def propertyType = <[ object ]>; // ConvertType(typePrefix, type, scope);
      
      _ = scope.DefineWithSource(
        if(scope.IsInterface)
          <[ decl: new $(property.Name : usesite) : $propertyType { get; set; }; ]>
        else
          <[ decl: public $(property.Name : usesite) : $propertyType { get; set; } ]>);
    }
    
    /// <summary>
    /// Don't pay attention to optional, always generate method
    /// </summary>
    CreateMethod(scope : TypeBuilder, method : Method, prefix : string) : void
    {
      def fullPrefix = string.Join(prefix, method.Name);
      def returnTypePrefix = string.Join(fullPrefix, Return);
      def realReturnType = <[ object ]>;;//ConvertType(returnTypePrefix, returnType, scope);
    
      def functionParams = ConvertParameters(scope, method.Signature.Parameters, fullPrefix);
    
      _ = scope.DefineWithSource(
        if(scope.IsInterface)
          <[ decl: new $(method.Name : usesite)(..$functionParams) : $realReturnType; ]>
        else
          <[ decl: 
            $(method.Name : usesite)(..$functionParams) : $realReturnType
              $DefaultImplementation
          ]>);
    }
    
    #endregion
    
    #region Converters
    
    private ConvertTypeArgument(
      scope : Scope,
      typeRef : ScriptType.Ref,
      typeParameters : IEnumerable[PT.Splicable]) : PExpr
    {
      // Name is either global or local, local is prefered
      def name =
        if(typeParameters.Cast.[PT.Splicable.Name]().Any(t => t.GetName().Id == typeRef.Name))
          PExpr.Ref(Name(typeRef.Name));
        else
          scope.FromQualifiedIdentifier(typeRef.Name);
      
      if(typeRef.TypeArguments is [])
        name
      else
      {
        def typeArguments = typeRef.TypeArguments
          .Select(tyArg => ConvertTypeArgumentImpl(scope, tyArg, typeParameters));
        <[ $name.[..$typeArguments] ]>
      }
    }
    
    /// <summary>
    /// Get PExpr from TS type
    /// </summary>
    private ConvertTypeArgumentImpl(
      scope : Scope,
      type : ScriptType,
      typeParameters : IEnumerable[PT.Splicable]) : PExpr
    {
      match(type)
      {
      | Any => <[ object ]>
      | Number => <[ float ]>
      | Boolean => <[ bool ]>
      | String => <[ string ]>
      | Void => <[ void ]>
      | Ref as r => ConvertTypeArgument(scope, r, typeParameters)
      | Query => Message.Error("Query type argument unsupported"); <[ ]>
      | Array => Message.Error("Array type argument unsupported"); <[ ]>
      | Object as o =>
        if(o.Members is [])
          <[ object ]>
        else
        {
          Message.Error("Object type argument is supported only without members"); <[ ]>
        }        
      | Function => Message.Error("Function type argument unsupported"); <[ ]>
      | Constructor => Message.Error("Constructor type argument unsupported"); <[ ]>
      | _ => Message.Error($"Unsupport type argument $type");  <[]>
      }
    }    
    
    private ConvertParameters(
      scope : TypeBuilder,
      parms : IEnumerable[ScriptParameter],
      prefix : string) : IEnumerable[PParameter]
    {      
      parms.Select(_ => <[ parameter: _ : object ]>)
    }
    
    #endregion
    
    #region Helpers
    
    /// <summary>
    /// We cannot simply put type as is because it leads to compilation problems.
    /// Assume following situation:
    /// module A { export module B { export class C { y : number; } } }
    ///
    /// module X {
    ///  export module A { export module B { export class C { x : number; } } }
    ///	
    ///  export var c : A.B.C;
    /// }
    ///
    /// TypeScript behaves like C# and allows this code with choosing internal module.
    /// We must do same lookup and find out all types starting from the current scope.
    /// </summary>
    private LookupTypeInScope(name : string, scope : Scope) : PExpr
    {
      // Try finding type going up in hierarcy of scope.
      def scopeFullName = scope.FullNameParts.ToArray();
      for(mutable i = 0; i < scopeFullName.Length - 1; i++)
      {
        def qid = scopeFullName.Take(scopeFullName.Length - i).JoinDot() + "." + name;
        when(scope.LookupType(qid) is Some(type))
          return type;
      }
      
      // If type has not been found use name
      <[ $(name : usesite) ]>
    }

    #endregion
    
    #region Special constants
    
    DefaultImplementation : PExpr = <[ { IgnoreParams(); throw ClientCodeCalledInServerException(); } ]>;
    
    private Call = "Call";
    private New = "New";
    private Variable = "Variable";
    private Constructor = "Constructor";
    private Index = "Index";
    private Return = "Return";
    private Parameter = "Parameter";
    private AnonymousType = "AnonymousType";
    private Separator = "_";
        
    #endregion
    
    #if A
    
    
    private GenerateTypes(scope : Scope, members : IEnumerable[DeclarationElement]) : void
    {
      foreach(mem in members) 
      { 
      | InterfaceDeclaration(value) => ()//CreateTSGlobalInterface(i, scope, false)
      //| Class     as c => CreateTSGlobalClass    (c, scope, false)
      //| Enum      as e => CreateTSGlobalEnum     (e, scope)
      //| Import    as i => CreateTSGlobalImport   (i, scope)
      //| Export    as e => CreateTSGlobalExport   (e, scope)
      //| Module    as m => CreateTSGlobalModule   (m, scope, true, false)
      | _ => ()
      }
    }
    private GenerateTypesMembers(scope : Scope, members : IEnumerable[TSGlobal]) : void
    {
      foreach(mem in members) 
      {
      | Interface as i => CreateTSGlobalInterface(i, scope, true)
      | Class     as c => CreateTSGlobalClass    (c, scope, true)
      | Module    as m => CreateTSGlobalModule   (m, scope, true, true)
      | _ => ()
      }
    }
    
    private GenerateOthers(
      scope : Scope,
      othersScope : TypeBuilder,
      members : IEnumerable[TSGlobal]) : void
    {
      foreach(mem in members)
      {
      | Module    as m => CreateTSGlobalModule   (m, scope, false, true)
      | Variable  as v => CreateTSGlobalVariable (v, othersScope, scope)
      | Function  as f => CreateTSGlobalFunction (f, othersScope, scope)
      | Construct as c => CreateTSGlobalConstruct(c, scope)
      | _ => ()
      }
    }
    
    private CreateTSGlobalVariable(var : TSGlobal.Variable, othersScope : TypeBuilder, scope : Scope) : void
    {
      def typePrefix = $"$(var.Name)_Variable";
      def varType = ConvertType(typePrefix, var.Type, scope);
      
      _ = othersScope.Define(<[ decl: public mutable $(var.Name : usesite) : $varType = default($varType); ]>);
    }
  
    /// <summary>
    /// Creates function in module.
    /// Global function can be without a name.
    /// </summary>
    private CreateTSGlobalFunction(function : TSGlobal.Function, othersScope : TypeBuilder, scope : Scope) : void
    {
      def typePrefix = $"$(function.Name)_Return";
      def returnType = ConvertType(typePrefix, function.ReturnType, scope);
        
      def functionParams = ConvertParameters(function.Name, function.Params, scope);
        
      _ = othersScope.Define(<[ decl: 
        public $(function.Name : usesite)(..$functionParams) : $returnType
          $DefaultImplementation
      ]>);   
    }
    
    private CreateTSGlobalInterface(tsInterface : TSGlobal.Interface, scope : Scope, typeMembers : bool) : void
    {
      def interfaceType =
        if (typeMembers || tsInterface.Extends is [])
          scope.Define(
            <[ decl: 
              [JSApi] public partial interface $(tsInterface.Name : usesite) {}
            ]>)
        else
        {
          def extends = tsInterface.Extends.Select(scope.FromQualifiedIdentifier);
          scope.Define(
            <[ decl:
              [JSApi] public partial interface $(tsInterface.Name : usesite) : ..$extends {}
            ]>)
        };
      
      when(typeMembers)
        foreach(member in tsInterface.Type.Members)
        {
        | Call(parms, returnType) =>
          def fullPrefix = $"$(tsInterface.Name)_Call";
          def returnTypePrefix = $"$(fullPrefix)_Return";
          def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
        
          def functionParams = ConvertParameters(fullPrefix, parms, scope);
       
          _ = interfaceType.DefineWithSource(
            <[ decl:
              new $(JSApiSpecial.Call : usesite)(..$functionParams) : $realReturnType;
            ]>);
        | Construct(parms, type) =>
          def fullPrefix = $"$(tsInterface.Name)_New";
          def typePrefix = $"$(fullPrefix)_Return";
          def realType = ConvertType(typePrefix, type, scope);
        
          def functionParams = ConvertParameters(fullPrefix, parms, scope);
          
          _ = interfaceType.DefineWithSource(
            <[ decl:
              new $(JSApiSpecial.New : usesite)(..$functionParams) : $realType;
            ]>);
        | Index(paramName, paramType, returnType) =>
          def (realParamType, typeName) =
            match(paramType)
            {
            | Number => (<[ float ]>, "Number")
            | String => (<[ string ]>, "String")
            };
        
          def fullPrefix = $"$(tsInterface.Name)_$(typeName)_Index";
          def returnTypePrefix = $"$(fullPrefix)_Return";
          def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
        
            _ = interfaceType.DefineWithSource(
            <[ decl:
              new Item[$(paramName : usesite) : $realParamType] : $realReturnType
              {
                get;
                set;
              }
            ]>);
        
        | Property(name, _optional, type) =>
          // Don't pay attention to optional, always generate property
          def fullPrefix = $"$(tsInterface.Name)_$(name)";
          def typePrefix = $"$(fullPrefix)_Property";
          def propertyType = ConvertType(typePrefix, type, scope);
        
          _ = interfaceType.DefineWithSource(<[ decl: new $(name : usesite) : $propertyType { get; set; }; ]>);
        
        | Function(name, _optional, parms, returnType) =>
          // Don't pay attention to optional, always generate property
          def fullPrefix = $"$(tsInterface.Name)_$(name)";
          def returnTypePrefix = $"$(fullPrefix)_Return";
          def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
        
          def functionParams = ConvertParameters(fullPrefix, parms, scope);
        
          _ = interfaceType.DefineWithSource(<[ decl: new $(name : usesite)(..$functionParams) : $realReturnType; ]>);
        }
        
      interfaceType.Compile();
    }
  
    private CreateTSGlobalClass(tsClass : TSGlobal.Class, scope : Scope, typeMembers : bool) : void
    {
      def classType = 
        if(typeMembers)
          scope.Define(<[ decl: [JSApi] public partial class $(tsClass.Name : usesite) {} ]>)
        else
          match((tsClass.Extends, tsClass.Implements))
          {
          | (None, []) =>
            scope.Define(<[ decl: [JSApi] public partial class $(tsClass.Name : usesite) {} ]>)
          | _ =>
            def extends =
              if (tsClass.Extends is Some(ex))
                if(scope.LookupType(ex) is Some(t))
                  [t]
                else []
              else [];
            
            def implement = tsClass.Implements.Select(scope.FromQualifiedIdentifier);
            def inherit = extends.Concat(implement);
            
            scope.Define(
              <[ decl:
                [JSApi] public partial class $(tsClass.Name : usesite) : ..$inherit {}
              ]>)
          };
      
      when(typeMembers)
        foreach(member in tsClass.Members)
        {
        | Constructor(parms) =>
        def fullPrefix = $"$(tsClass.Name)_Constructor";
        def functionParams = ConvertParameters(fullPrefix, parms, scope);
        
        _ = classType.DefineWithSource(
          <[ decl:
            public this(..$functionParams)
              $DefaultImplementation
          ]>);
      
        | Member(part, isStatic, _access) =>
        // Access is ignored.      
        // Generate private as public ones.
        match(part)
        {
        | Variable(name, type) =>
          def fullPrefix = $"$(tsClass.Name)_$(name)";
          def typePrefix = $"$(fullPrefix)_Variable";
          def variableType = ConvertType(typePrefix, type, scope);
          
          if(isStatic is Some(true))
            _ = classType.DefineWithSource(
              <[ decl: public static mutable $(name : usesite) : $variableType = default($variableType); ]>);
          else
            _ = classType.DefineWithSource(
              <[ decl: public mutable $(name : usesite) : $variableType = default($variableType); ]>);
          
        | Method(name, _optional, parms, returnType) =>
          // Don't pay attention to optional, always generate property
          def fullPrefix = $"$(tsClass.Name)_$(name)";
          def returnTypePrefix = $"$(fullPrefix)_Return";
          def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
            
          def functionParams = ConvertParameters(fullPrefix, parms, scope);
          
          if(isStatic is Some(true))
            _ = classType.DefineWithSource(
              <[ decl: 
                public static new $(name : usesite)(..$functionParams) : $realReturnType
                  $DefaultImplementation
              ]>);
          else
            _ = classType.DefineWithSource(
              <[ decl: 
                public new $(name : usesite)(..$functionParams) : $realReturnType
                  $DefaultImplementation
              ]>);
        }
        }
        
      classType.Compile();
    }
  
    private CreateTSGlobalConstruct(_c : TSGlobal.Construct, _scope : Scope) : void
    {
      Message.Hint("TSGlobal.Construct is not supported");
    }
    
    private CreateTSGlobalEnum(tsEnum : TSGlobal.Enum, scope : Scope) : void
    {
      def type = <[ decl: public enum $(tsEnum.Name : usesite) { } ]>;
      scope.Define(type).Compile();
    }
  
    private CreateTSGlobalImport(_i : TSGlobal.Import, _scope : Scope) : void
    {
      Message.Hint("TSGlobal.Import is not supported");
    }
  
    private CreateTSGlobalExport(_e : TSGlobal.Export, _scope : Scope) : void
    {
      Message.Hint("TSGlobal.Export is not supported");
    }
  
    private CreateTSGlobalModule(tsModule : TSGlobal.Module, scope : Scope, types : bool, typeMembers : bool) : void
    {
      def moduleName = tsModule.Name.Replace('/', '.').Replace('\\', '.');

      // Create modules by splitting '.'
      def moduleType = CreateModule(moduleName.SplitDot(), scope, tsModule.Members, types, typeMembers);
      moduleType.Compile()
    }
    
    
           

    #region Helpers
    
    /// <summary>
    /// Return parameter types
    /// </summary>
    private GetParamsType(prefix : string, parms : list[TSParam], scope : Scope) : list[PExpr]
    {
      def makePrefix(name)
      {
        $"$(prefix)_$(name)_Parameter"
      }
      def convertParamsType(p : TSParam)
      {
        | Required(name, type, _access)
        | Option(name, _, type, _access) => ConvertType(makePrefix(name), type, scope)
        | Rest(name, type) => ConvertType(makePrefix(name), type, scope)
      }
    
      def ret = parms.Map(convertParamsType);
      ret
    }

    /// <summary>
    /// Convert function parameters
    /// </summary>
    private ConvertParameters(prefix : string, parms : IEnumerable[TSParam], scope : Scope) : IEnumerable[PParameter]
    {
      def makePrefix(name)
      {
        $"$(prefix)_$(name)_Parameter"
      }
      def convertParamsType(p : TSParam)
      {
        | Required(name, type, _access)
        | Option(name, _optional, type, _access) =>
          def realType = ConvertType(makePrefix(name), type, scope);
          <[ parameter: $(name : usesite) : $realType ]>          
        | Rest(name, type) =>
          def realType = ConvertType(makePrefix(name), type, scope);
          <[ parameter: params $(name : usesite) : $realType ]>
      }
    
      def ret = parms.Select(convertParamsType);
      ret
    }
    
    private CreateAnonymousType(prefix : string, members : list[TSObject], scope : Scope) : PExpr
    {
      if(members is [])
        <[ object ]>
      else
        CreateAnonymousTypeAlways(prefix, members, scope)
    }
    
    static mutable anonymousTypeIndex : int = 0;
    
    private CreateAnonymousTypeAlways(prefix : string, members : list[TSObject], scope : Scope) : PExpr
    {
      // Handle no members case
      
      def classNamePrefix = $"$(prefix)_AnonymousType";
      
      anonymousTypeIndex++;
      def className = $"$classNamePrefix$anonymousTypeIndex";
      
      def anonClass = scope.Define(<[ decl:
        [JSApi] public sealed class $(className : usesite) { }
      ]>);
      
      // Suppress warning
      anonClass.HasBeenUsed = true;
      
      // Add members
      def analyzedMembers = members.SelectMany(AnalyzeTSObject).Distinct(TSObjectEqualityComparer());
      foreach(member in analyzedMembers)
      {
      | Call(parms, returnType) =>
        def fullPrefix = $"$(className)_Call";
        def returnTypePrefix = $"$(fullPrefix)_Return";
        def realReturnType = ConvertType(returnTypePrefix, returnType, scope);

        def functionParams = ConvertParameters(fullPrefix, parms, scope);
        
        _ = anonClass.DefineWithSource(
          <[ decl:
            public $(JSApiSpecial.Call : usesite)(..$functionParams) : $realReturnType
              $DefaultImplementation
          ]>);
      | Construct(parms, type) =>
        def fullPrefix = $"$(className)_New";
        def typePrefix = $"$(fullPrefix)_Return";
        def realType = ConvertType(typePrefix, type, scope);
        
        def functionParams = ConvertParameters(fullPrefix, parms, scope);
          
        _ = anonClass.DefineWithSource(
          <[ decl:
            public $(JSApiSpecial.New : usesite)(..$functionParams) : $realType
              $DefaultImplementation
          ]>);
      | Index(paramName, paramType, returnType) =>
        def (realParamType, typeName) =
          match(paramType)
          {
          | Number => (<[ float ]>, "Number")
          | String => (<[ string ]>, "String")
          };
        
        def fullPrefix = $"$(className)_$(typeName)_Index";
        def returnTypePrefix = $"$(fullPrefix)_Return";
        def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
        
        _ = anonClass.DefineWithSource(
          <[ decl:
            public Item[$(paramName : usesite) : $realParamType] : $realReturnType
            {
              get
                $DefaultImplementation;
              set
                $DefaultImplementation;
            }
          ]>);
      
      | Property(name, _optional, type)  =>
        // Don't pay attention to optional, always generate property
        def fullPrefix = $"$(className)_$(name)";
        def typePrefix = $"$(fullPrefix)_Property";
        def propertyType = ConvertType(typePrefix, type, scope);
        
        _ = anonClass.DefineWithSource(<[ decl: public $(name : usesite) : $propertyType { get; set; } ]>);
      
      | Function(name, _optional, parms, returnType) =>
        // Don't pay attention to optional, always generate property
        def fullPrefix = $"$(className)_$(name)";
        def returnTypePrefix = $"$(fullPrefix)_Return";
        def realReturnType = ConvertType(returnTypePrefix, returnType, scope);
        
        def functionParams = ConvertParameters(fullPrefix, parms, scope);
        
        _ = anonClass.DefineWithSource(
          <[ decl: 
            $(name : usesite)(..$functionParams) : $realReturnType
              $DefaultImplementation
          ]>);
      }
      
      // Finalize
      anonClass.Compile();
      <[ $(anonClass.Name : usesite) ]>
    }
  
    private ConvertType(prefix : string, inputType : option[TSType], scope : Scope) : PExpr
    {
      match(inputType)
      {
      | Some(type) => ConvertType(prefix, type, scope)
      | None => <[ object ]>
      }
    }
  
    private ConvertType(prefix : string, inputType : TSType, scope : Scope) : PExpr
    {
      match(inputType)
      {
      | Array(type, 0) => <[ $(ConvertType(prefix, type, scope)) ]>
      | Array(type, rank) => <[ array[$(ConvertType(prefix, TSType.Array(type, rank - 1), scope))] ]>
      | Bool => <[ bool ]>   
      | Lambda(parms, ret) =>
        mutable lambdaParams = GetParamsType(prefix, parms, scope);
        def type =
          if(ret is TSType.Void())
            <[ System.Action ]>
          else
          {
            lambdaParams = lambdaParams.Concat([ConvertType(prefix, ret, scope)]);
            <[ System.Func ]>
          };
        
        <[ $type.[..$lambdaParams] ]>
      | Named(name) => LookupTypeInScope(name, scope)
      | New(parms, returnType) =>
        CreateAnonymousType(prefix, [TSObject.Construct(parms, Some(returnType))], scope)
        
      | Number => <[ float ]>
      | Object(members) => CreateAnonymousType(prefix, members, scope)
      | String => <[ string ]>
      | Void => <[ void ]>
      | Any => <[ object ]>
      }
    }
    
    // <summary>
    // We cannot simply put type as is because it leads to compilation problems.
    // Assume following situation:
    // module A { export module B { export class C { y : number; } } }
    //
    // module X {
    //  export module A { export module B { export class C { x : number; } } }
    //	
    //  export var c : A.B.C;
    //}
    //
    // TypeScript behaves like C# and allows this code with choosing internal module.
    // We must do same lookup and find out all types starting from the current scope.
    // </summary>
    private LookupTypeInScope(name : string, scope : Scope) : PExpr
    {
      // Try finding type going up in hierarcy of scope.
      // TODO: Think about functional solution :)
      def scopeFullName = scope.FullNameParts.ToArray();
      for(mutable i = 0; i < scopeFullName.Length - 1; i++)
      {
        def qid = scopeFullName.Take(scopeFullName.Length - i).JoinDot() + "." + name;
        when(scope.LookupType(qid) is Some(type))
          return type;
      }
      
      // If type has not been found use name
      <[ $(name : usesite) ]>
    }
    
    private CreateModule(
      parts : list[string],
      outer : Scope,
      members : list[TSGlobal],
      types : bool,
      typeMembers : bool) : TypeBuilder
    {
      match (parts)
      {
      | [] => 
        if(outer.TypeBuilder != null) outer.TypeBuilder 
        else
        {
          Message.Error("List must not be empty");
          null
        }
        
      | [hd] =>
        // One part means we generate members
        // Generate partial module to simplify the code
        // Otherwise we need to lookup for type in case types == false
        def moduleType = outer.Define(<[ decl: [JSApi] public partial module $(hd : usesite) {} ]>);
        
        if(types)
          if(!typeMembers)
            GenerateTypes(Scope(moduleType), members);
          else
            GenerateTypesMembers(Scope(moduleType), members);            
        else
          GenerateOthers(Scope(moduleType), moduleType, members);
        
        moduleType
      
      | hd :: tl =>
        // If module consists from more than one part, first create all needed parts,
        // and then generate all members
        // Generate partial module to simplify the code
        // Otherwise we need to lookup for type in case types == false
        def innerModule = outer.Define(<[ decl: [JSApi] public partial module $(hd : usesite) {} ]>);
        def t = CreateModule(tl, Scope(innerModule), members, types, typeMembers);
        innerModule.Compile();
        t
      }
    }

    private FailedParsing(tsFile : string, text : string, pos : int) : void
    {
      def errorText =
        if(pos + 55 > text.Length)
          text.Substring(pos)
        else
          text.Substring(pos, 55);
      Message.Error(Location(tsFile, 1, 1, 1, 1), $"Failed to parse $tsFile, somewhere here: $errorText...");
    }
    
    #endregion
    
    #endif
    
  }
}
