using NemerleWeb;

using Nemerle.Extensions;
using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Website.Samples
{
  [Unit]
  public class CalculatorPage
  {
    _calc : Calculator.CalcParser;
    
    public this()
    {
      _calc = Calculator.CalcParser();
      CalcInput = "1 + 2 - 3 * 4 / 5";
    }
         
    CalcOutput : string
    {
      get
      {
        try
        {
          if (!string.IsNullOrEmpty(CalcInput))
            if (_calc.Parse(CalcInput) is Some(res))
              res.ToString()
            else
              "Incorrect input"
          else
            "";
        }
        catch
        {
        | e => e.Message
        }
      }
    }
     
    public CalcInput : string { get; set; }
    
    [Html]
    public View() : string
    {
      <#
        <div>
          <span>This is demonstration of Calculator parser</span><br />
          <span>Write any valid expression.</span><br />
          <span>Example: 1 + 2 - 3 *  4</span>
          <div class="output">
            <span class="output-text">$CalcOutput</span> 
          </div>
          <div class="calc-input">
            <input value="$CalcInput"/>
          </div>
        </div>
      #>
    }    
  }
}
