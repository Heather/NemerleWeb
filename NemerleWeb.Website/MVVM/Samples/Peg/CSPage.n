using NemerleWeb;

using Nemerle.Extensions;
using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Website.Samples
{
  [Unit]
  public class CSPage
  {
    _parser : CSharpParser.Parser;
    
    public this()
    {
      _parser = CSharpParser.Parser();
      ParserInput = <#using System;
using System.Collections.Generic;
using System.Linq;

interface ITest
{
    void F();
}

class Test : ITest
{
    public void F() { }
}

class Program
{
    static void Main()
    {
    }
}#>;
    }
         
    ParserOutput : string
    {
      get
      {
        if (!string.IsNullOrEmpty(ParserInput))
          if (_parser.Parse(ParserInput) is Some(res))
          {
            $<#Extern aliases count: $(res.ExternAliases.Length),
Usings count: $(res.UsingDirectives.Length),
Global attributes count: $(res.GlobalAttributes.Length),
Members count: $(res.Members.Length)
#>
          }
          else
            "Incorrect input"
        else
          "";
      }
    }
     
    public ParserInput : string { get; set; }
    
    [Html]
    public View() : string
    {
      <#
        <div>
          <span>This is demonstration of C# parser</span><br />
          <div class="output">
            <span class="output-text">$ParserOutput</span> 
          </div>
          <div class="parser-input">
            <textarea rows="6" cols="50" value="$ParserInput"/>
          </div>
        </div>
      #>
    }
  }
}
