using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using NemerleWeb;
using NemerleWeb.TypedJS;

namespace NemerleWeb.Rsdn
{  
  [Unit]
  public class TreeNode
  {
    public Id : string { get; set; }
    public Caption : string { get; set; }
    public Icon : string { get; set; }
    public Hide : bool { get; set; }
    public Depth : int { get; set; }
    public IsOpened : bool { get; set; }
    public Children : List[TreeNode] { get; set; }    
    public HasChildren : bool { get; set; }
    
    public Parent : TreeNode { get; set; }
    public IsLoading : bool { get; set; }    
    public IsSelected : bool { get; set; }
    
    mutable _href : string;
    public Href : string 
    { 
      get { if(_href == null || _href.StartsWith("http://")) _href else "http://www.rsdn.ru" + _href }; 
      set { _href = value }; 
    }
    
    public IconUrl : string 
    {      
      get {        
        def suffix = match(IsOpened, HasChildren) {
                      | (false, true) => "c.gif" 
                      | (true, _) => "o.gif";
                      | _ => "s.gif";
                     };
        "http://rsdn.ru/images/tree/" + Icon + suffix;
      }
    }
        
    public TogglePin() : void
    {            
      if(!MainPage.Instance.IsPinned(this)) MainPage.Instance.AddPinned(Id)
      else MainPage.Instance.RemovePinned(Id)
    }
    
    public CaptionClick() : void
    {
      if(HasChildren) {
        if(Children == null) {
          IsLoading = true;
          IsOpened = !IsOpened;
          _ = server.LoadNodeChildren(Id, children => {
            Children = children;
            
            foreach(child in Children)
              child.Parent = this;
              
            IsLoading = false;
          });
        } else
          IsOpened = !IsOpened;
      }
      else { 
        if(window.@event.ctrlKey)
          _ = window.open(Href, "_blank", null, false);
        else
          MainPage.Instance.SelectNode(this);
      }
    }
    
    [Html]
    public View() : string
    {
      <#
        <div $foreach(c in Children) style-margin-left="$(c.Depth * 6)" class="node" css-selected-search-result="$(c.IsSelected)">
          <a click="$(c.CaptionClick())" href="$("http://www.rsdn.ru" + c.Href)" css-with-children="$(c.HasChildren)">
            <img class="node-icon" src="$(c.IconUrl)" />
            <span class="node-caption" css-is-active="$(MainPage.Instance.IsActiveNode(c))">
              $(c.Caption)
            </span>
            <div $when(!c.HasChildren) click="$(c.TogglePin())" class="node-pin" css-pinned="$(MainPage.Instance.IsPinned(c))" />
          </a>
          <div $when(c.IsLoading) class="node-loading">
            Загрузка, пожалуйста подождите...
          </div>
          <div $when(c.IsOpened && Children != null)>
            <div template="$c" />
          </div>
        </div>
      #>
    }
    
    public class Server
    {
      public LoadNodeChildren(id : string) : List[TreeNode]
      {
        TreeLoader.LoadNodeChildren(id)
      }
    }
  }
}
