using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;
using NemerleWeb;
using NemerleWeb.TypedJS;

using Rsdn.Tree.Models;

namespace Rsdn
{    
  [Unit]
  public class TopicList
  {
    [Dto] public class Topic { Name : string; Author : string; Rating : string; Comments : string; Url : string }

    private mutable _url : string;
    private mutable _topics : List[Topic];
    private mutable _selectedTopic : CommentNode;

    public Url : string 
    { 
      get { _url; } 
      set { 
        _url = value; 
        _ = server.GetTopicList(value, topics => _topics = topics.ToList());
      }
    }

    OpenTopic(url : string) : void
    {
      _ = server.LoadReplies(url, node => _selectedTopic = node);
    }

    [Html]
    public View() : string
    {
      <#
        <div $foreach(topic in _topics) class="topic" click="$(OpenTopic(topic.Url))" visible="$(_selectedTopic == null)">
          <span class="topic-name">$(topic.Name)</span>
          <span class="topic-comment-count">Ответов: $(topic.Comments)</span>
          <span class="topic-author">Автор: $(topic.Author)</span>
          <span class="topic-rating">Рейтинг: $(topic.Rating)</span>
        </div>
        <div $when(_selectedTopic != null)>
          <div template="$_selectedTopic" />
        </div>
      #>
    }
    
    public class Server
    {
      public GetTopicList(url : string) : IEnumerable[Topic]
      {
        TopicLoader.GetTopicList(url);
      }

      public LoadReplies(url : string) : CommentNode
      {
        TopicLoader.LoadReplies(url)
      }
    }
  }
}
