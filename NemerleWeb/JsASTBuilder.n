using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  public module JsAstBuilder
  {
      Build(expr : TExpr) : JsAst
      {
        match(expr) 
        {
        | Array(args, _dims)                => JsAst.Array(args.Map(Build))
        | ArrayIndexer(obj, a :: _)         => JsAst.ArrayIndexer(Build(obj), Build(a))
        | Assign(s, t)                      => JsAst.Assign(Build(s), Build(t))
        | Base                              => Message.Warning(expr.Location, "Base not implemented"); JsAst.NotImpl("Base")
        | Block(body = body)                => Build(body) 
        | Cache                             => Message.Warning(expr.Location, "Cache not implemented"); JsAst.NotImpl("Cache")
        | CacheRef                          => Message.Warning(expr.Location, "CacheRef not implemented"); JsAst.NotImpl("CacheRef")
        | Call as call                      => JsKnownMembers.Convert(call, Build)
        | ClosureFieldOf                    => Message.Warning(expr.Location, "ClosureFieldOf not implemented"); JsAst.NotImpl("ClosureFieldOf")
        | ClosureObjectOf                   => Message.Warning(expr.Location, "ClosureObjectOf not implemented"); JsAst.NotImpl("ClosureObjectOf")
        | ConstantObjectRef                 => Message.Warning(expr.Location, "ConstantObjectRef not implemented"); JsAst.NotImpl("ConstantObjectRef") 
        | CtorOf                            => Message.Warning(expr.Location, "CtorOf not implemented"); JsAst.NotImpl("CtorOf")
        | DebugInfo(expr = expr)            => Build(expr)
        | Def                               => Message.Warning(expr.Location, "Def not implemented"); JsAst.NotImpl("Def")
        | DefaultValue                      => JsAst.Void()
        | DefFunctionsIn                    => Message.Warning(expr.Location, "DefFunctionsIn not implemented"); JsAst.NotImpl("DefFunctionsIn")
        | DefValIn(name, val, body)         => JsAst.Seq([JsAst.DefValue(name.Name, Build(val)), Build(body)])
        | Delayed                           => Message.Warning(expr.Location, "Delayed not implemented"); JsAst.NotImpl("Delayed")
        | Error                             => Message.Warning(expr.Location, "Error not implemented"); JsAst.NotImpl("Error")
        | EventMember                       => Message.Warning(expr.Location, "EventMember not implemented"); JsAst.NotImpl("EventMember")
        | FieldMember(obj, fld)             => JsKnownMembers.ConvertField(obj, fld, Build)
        | FieldOf                           => Message.Warning(expr.Location, "FieldOf not implemented"); JsAst.NotImpl("FieldOf")
        | Goto(target, _)                   => JsAst.Goto(target)
        | HasType(expr, type)               => JsAst.HasType(Build(expr), JsClass.GetProperName(type.TypeInfo))
        | If(cond, then, els, _, _)         => JsAst.If(Build(cond), Build(then), Build(els))
        | ImplicitValueTypeCtor             => Message.Warning(expr.Location, "ImplicitValueTypeCtor not implemented"); JsAst.NotImpl("ImplicintValueTypeCtor")
        | Label(id, body)                   => JsAst.Label(id, Build(body))
        | Literal(val)                      => 
            match(val) {                
            | Null          => JsAst.Literal(JsLiteral.Null())
            | String (val)  => JsAst.Literal(JsLiteral.String(val))
            | Float (val)   => JsAst.Literal(JsLiteral.Float(val))
            | Double (val)  => JsAst.Literal(JsLiteral.Float(val))
            | Decimal (val) => JsAst.Literal(JsLiteral.Float(Convert.ToDouble(val)))
            | Char (val)    => JsAst.Literal(JsLiteral.String(val.ToString()))
            | Bool (val)    => JsAst.Literal(JsLiteral.Bool(val))
            | Integer (val, isneg, _) => JsAst.Literal(JsLiteral.Integer((val :> int) * (if(isneg) -1 else 1)))
            | Enum => 
                Message.Error("Enums currently not supported");
                JsAst.NotImpl();
            | Void          => JsAst.Void()
            | _ => Message.Error("Unmatched literal"); JsAst.NotImpl();
            }
        | LocalFunRef                       => Message.Warning(expr.Location, "LocalFunRef not implemented"); JsAst.NotImpl("LocalFunRef")
        | LocalRef(decl)                    => JsAst.LocalRef(decl.Name)
        | MacroEnvelope(expanded = ex, original = org, the_macro = _) => 
            match(org) { 
            /*| <[ foreach($it in $coll) $body  ]> => 
                def it = it.ToString();
                def coll = if(coll.TypedObject is TExpr) Build(coll.TypedObject :> TExpr) else JsAst.LocalRef(coll.ToString());
                def body = match(body) {
                | Sequence(h :: _) => if(h.TypedObject is TExpr) Build(h.TypedObject :> TExpr) else JsAst.Code(h.ToString());
                | pexpr => if(pexpr.TypedObject is TExpr) Build(pexpr.TypedObject :> TExpr) else JsAst.Code(pexpr.ToString());
                }
                    
                JsAst.For(it, coll, body);      */              
            | MacroCall(Name(Id = "js"), _, p :: []) => 
                match(p) {
                | SyntaxElement.Expression(Literal(Literal.String(val))) => JsAst.Code(val)
                | _ => JsAst.Code(p.ToString().Trim('\"'));
                }                    
            | _ => Build(ex)
            }                
        | Match(expr, _)                    => Message.Warning(expr.Location, "Match not implemented"); JsAst.NotImpl("Match")
        | MethodAddress                     => JsAst.Literal(JsLiteral.String(""))
        | MethodOf                          => Message.Warning(expr.Location, "MethodOf not implemented"); JsAst.NotImpl("MethodOf")
        | MethodRef(obj, meth, _, _)        => 
            if(meth.Name.StartsWith("get_") || meth.Name.StartsWith("set_"))
                JsAst.MethodRef(Build(obj), meth.Name.Substring(4))
            else
                JsAst.MethodRef(Build(obj), meth.Name)
        | MultipleAssign(assigns)           => JsAst.Seq(assigns.Map((l, e) => JsAst.DefValue(l.Name, Build(e))))
        | OpCode(name)                      => ConvertToJsOpCode(name)
        | PropertyMember(obj, prop)         => JsAst.PropertyGet(Build(obj), prop.Name, JsClass.HasGetterNoSetter(prop))
        | SelfTailCall                      => Message.Warning(expr.Location, "SelfTailCall not implemented"); JsAst.NotImpl("SelfTailCall")
        | Sequence(e1, e2)                  => JsAst.Seq([Build(e1), Build(e2)])
        | StaticEventRef                    => Message.Warning(expr.Location, "StaticEventRef not implemented"); JsAst.NotImpl("StaticEventRef")
        | StaticPropertyRef                 => Message.Warning(expr.Location, "StaticPropertyRef not implemented"); JsAst.NotImpl("StaticPropertyRef")
        | StaticRef(from, mem, _)           => 
          def typeName = if(from.tycon.SystemType != null) from.tycon.SystemType.FullName else from.tycon.FrameworkTypeName;              
          match(typeName) {
          | _ when typeName == typeof(Nemerle.Core.list[_].Nil).FullName => JsAst.Array([])
          | _ => JsAst.StaticRef(JsClass.GetProperName(from.TypeInfo), mem.Name);
          }
        | Switch(expr, defCase, cases)      => JsAst.Switch(Build(expr), cases.Map((id, ex) => JsAst.SwitchCase(id, Build(ex))), if(defCase.IsSome) Build(defCase.Value) else JsAst.Void())
        | This                              => JsAst.This()
        | Throw(expr)                       => JsAst.Throw(Build(expr))
        | Try(_, _)                         => Message.Warning("Try is not implmemented"); JsAst.NotImpl("Try");
        | TryFinally(body, handler)         => JsAst.TryFinally(Build(body), Build(handler))
        | Tuple(args)                       => JsAst.Tuple(args.Map(a => Build(a)))
        | TupleIndexer(obj, pos, _len)      => JsAst.TupleIndexer(Build(obj), pos)
        | TypeConversion(e, _, _, _)        => Build(e)
        | TypeOf                            => JsAst.NotImpl("TypeOf")
        | Use                               => JsAst.NotImpl("Use")
        | _ => Message.Error("Unmatched TExpr $expr"); JsAst.NotImpl("unmatched");
        }
      } 
      
      ConvertToJsOpCode(name : string) : JsAst
      {
        | "+.s"                       
        | "+.u"                       
        | "+.f"                       => JsAst.OpCode("+")
        | "<<.s" | "<<.f"                
        | "<<.u"                      => JsAst.OpCode("<<")
        | ">>.s" | ">>.f"             
        | ">>.u"                      => JsAst.OpCode(">>")
        | "unary.+.s"                    
        | "unary.+.u"                    
        | "unary.+.f"                 => JsAst.OpCode("+")
        | "-.s"                       
        | "-.u"                       
        | "-.f"                       
        | "unary.-.s"                 
        | "unary.-.f"                 => JsAst.OpCode("-")
        | "unary.~.u"                    
        | "unary.~.s"                 => JsAst.OpCode("~")
        | "++.i4.f"                   
        | "++.i4.s"                   
        | "++.i4.u"                   
        | "++.i4.s.i1"                
        | "++.i4.u.i1"                
        | "++.i4.s.i2"                
        | "++.i4.u.i2"                
        | "++.i8.f"                   
        | "++.i8.s"                   
        | "++.i8.u"                   
        | "++.r4.f"                   
        | "++.r8.f"                   => JsAst.OpCode("++")
        | "--.i4.f"                   
        | "--.i4.s"                   
        | "--.i4.u"                   
        | "--.i4.s.i1"                
        | "--.i4.u.i1"                
        | "--.i4.s.i2"                
        | "--.i4.u.i2"                
        | "--.i8.f"                   
        | "--.i8.s"                   
        | "--.i8.u"                   
        | "--.r4.f"                   
        | "--.r8.f"                   => JsAst.OpCode("--")
        | "unary.~.f"                 => JsAst.OpCode("~")
        | "*.s"                       
        | "*.u"                       
        | "*.f"                       => JsAst.OpCode("*")
        | "/.s" | "/.f"               
        | "/.u"                       => JsAst.OpCode("/")
        | "%.s" | "%.f"               
        | "%.u"                       => JsAst.OpCode("%")
        | "|.s" | "|.u" | "|.f" | "|" => JsAst.OpCode("|")
        | "&.s" | "&.u" | "&.f" | "&" => JsAst.OpCode("&")
        | "^.s" | "^.u" | "^.f" | "^" => JsAst.OpCode("^")
        | "bool.!"                    => JsAst.OpCode("!")
        | "==.ref" | "=="             => JsAst.OpCode("===")
        | "!=.ref" | "!="             => JsAst.OpCode("!==")
        | "<.s" | "<.f"               
        | "<.u"                       => JsAst.OpCode("<")
        | ">.s" | ">.f"               
        | ">.u"                       => JsAst.OpCode(">")
        | "<=.s"                      
        | "<=.f"                      
        | "<=.u"                      => JsAst.OpCode("<=")
        | ">=.s"                      
        | ">=.f"                      
        | ">=.u"                      => JsAst.OpCode(">=")
        | _                           => 
            Message.Error("unmatched TExpr.Opcode (" + name + ")"); 
            JsAst.NotImpl();
      }
      
      TryToConvertKnownTemplates(expr : TExpr) : option[JsAst]
      {
        match (expr)
        {
        // Use Enumerator
        | DefValIn(LocalValue as lv, Call(func = MethodRef(meth = getenum, obj = collection)), body)
          when lv.Name == "enumerator" && getenum.Name == "GetEnumerator" =>
            
          match(body)
          {
          // try { (); if (MoveNext) Current } finally { _.Dispose() }
          | TryFinally(
              Sequence(Literal, Label(body = MacroEnvelope(expanded = ifexpr))),
              Call(func = MethodRef(meth = disp)))
            when disp.Name == "Dispose" => 
              
            match(ifexpr)
            {
            | If(
                cond = Call(OpCode("=="),
                [Parm(expr = TypeConversion(expr = Call(func = MethodRef(meth = movnxt)))),
                  Parm(expr = TypeConversion(expr = Literal(Literal.Bool(true))))],
                _), e_then = ifbody) when movnxt.Name == "MoveNext" => 
              
              match(ifbody)
              {
              | Label(
                  _,
                  TypeConversion(
                  expr = Sequence(DefValIn(body = DefValIn(body = Sequence(_, foreachbody)), name = iterName), _))) =>
                Some(JsAst.For(iterName.Name, Build(collection), Build(foreachbody))) 
              | _ => None()
              }            
            | _ => None()
            }
          // try { (); (defvalin; if (MoveNext) Current) } finally { _.Dispose() }
          | TryFinally(
              Sequence(Literal, Label(body = DefValIn(body = MacroEnvelope(expanded = ifexpr)) as defValIn)),
              Call(func = MethodRef(meth = disp)))
            when disp.Name == "Dispose" => 
              
            match(ifexpr)
            {
            | If(
                cond = Call(OpCode("=="),
                [Parm(expr = TypeConversion(expr = Call(func = MethodRef(meth = movnxt)))),
                  Parm(expr = TypeConversion(expr = Literal(Literal.Bool(true))))],
                _), e_then = ifbody) when movnxt.Name == "MoveNext" => 
              
              match(ifbody)
              {
              | Label(
                  _,
                  TypeConversion(
                  expr = Sequence(DefValIn(body = DefValIn(body = Sequence(_, foreachbody)) as cache, name = iterName), _))) =>
                    Some(JsAst.Seq([JsAst.DefValue(defValIn.name.Name, Build(defValIn.val)), 
                                    JsAst.For(iterName.Name, 
                                              Build(collection), 
                                              JsAst.Seq([JsAst.DefValue(cache.name.Name, JsAst.LocalRef(iterName.Name)), 
                                                         Build(foreachbody)]))])) 
              | _ => None()
              }            
            | _ => None()
            }
            
          // try { (); if (MoveNext) Current } finally { _.Dispose() }
          | TryFinally(
              Sequence(
                Literal,
                DefValIn(name = name, val = val, body = Label(body = MacroEnvelope(expanded = ifexpr)))),
              Call(func = MethodRef(meth = disp)))
             when disp.Name == "Dispose" =>              
            
            match (ifexpr)
            {
            | If(
                cond = Call(OpCode("=="),
                [Parm(expr = TypeConversion(expr = Call(func = MethodRef(meth = movnxt)))),
                  Parm(expr = TypeConversion(expr = Literal(Literal.Bool(true))))],
                _), e_then = ifbody) when movnxt.Name == "MoveNext" => 
             
             match(ifbody)
              {
              | Label(
                  _,
                  TypeConversion(
                  expr = Sequence(DefValIn(body = DefValIn(body = Sequence(_, foreachbody)), name = iterName), _))) =>
                Some(
                  JsAst.Seq
                  ([
                    JsAst.DefValue(name.Name, Build(val)),
                    JsAst.For
                    (
                      iterName.Name,
                      Build(collection),
                      JsAst.Seq
                      ([
                        Build(foreachbody),
                        JsAst.Call(JsAst.OpCode("++"), [JsAst.LocalRef(name.Name)])
                      ]))
                  ]))
              | _ => None()
              }
            | _ => None()
            }
          | _ => None() 
          }
        | _ => None()
        }
      }

      public BuildJsAstForSplices(expr : TExpr) : list[JsAst.DefValue]
      {
          mutable JsAst = JsAstBuilder.Build(expr);
          JsAst = JsAstHelper.FlattenAndRemoveVoids(JsAst);
          JsAstHelper.IterateAST(JsAst)
                     .OfType.[JsAst.DefValue]().NToList()
                     .Map(j => match(j) {                     
                     | JsAst.DefValue(name, FieldRef(left, prop, false)) => JsAst.DefValue(name, JsAst.FieldRef(left, prop, true))
                     | JsAst.DefValue(name, Call(MethodRef(Method = "template") as template, [PropertyGet(l1, r1, false), PropertyGet as p2])) => 
                        JsAst.DefValue(name, JsAst.Call(template, [JsAst.FieldRef(l1, r1, true), p2]))
                     | _ => j
                     });
      }
      
      public BuildJsAst(expr : TExpr, shouldReturn : bool) : JsAst
      {
        mutable JsAst = JsAstBuilder.Build(expr);        
        
        JsAst = JsAstHelper.LabelsToNamedWhile(JsAst);
        JsAst = JsAstHelper.TransformBottomUp(JsAst);
        JsAst = JsAstHelper.FlattenAndRemoveVoids(JsAst);
        JsAst = JsAstHelper.RemoveEmptyBranches(JsAst);
            
        when(shouldReturn)
            JsAst = JsAstHelper.MakeReturn(JsAst);
        
        match(JsAst) {
        | Seq => JsAst
        | _ => JsAst.Seq([JsAst])
        }
      }
  }
}
