using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;
using NemerleWeb;

using System;
using System.Text;
using System.IO;
using System.Reflection;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;
using System.Runtime.Serialization;
using System.Security.Cryptography;

namespace NemerleWeb
{
  public module NWebData
  {
    public Scripts : List[string] = List();
    public Templates : List[(string * string)] = List();   
    public CombinedScriptsFilename : string;
    public ResourcesFilename : string;
    
    this() 
    {
      def readResource(name, assembly) {        
        using (def stream = assembly.GetManifestResourceStream(name))
        using (def reader = StreamReader(stream))       
            reader.ReadToEnd();        
      }
      def datas = AppDomain.CurrentDomain
                           .GetAssemblies()
                           .SelectMany(a => a.GetTypes()
                           .Where(t => t.Name.EndsWith("NWebDataPerAssembly")));
                           
      def staticFields = StringBuilder();
      def combinedScripts = StringBuilder();
      
      def getRelativePath(filename = "")
      {
        if(filename != "") Path.Combine("Scripts", "NemerleWeb", filename)
        else Path.Combine("Scripts", "NemerleWeb")
      }
      
      def getAbsolutePath(filename = "") 
      {
        Path.Combine(AppDomain.CurrentDomain.BaseDirectory, getRelativePath(filename))
      }

      Directory.GetFiles(getAbsolutePath())
               .Iter(File.Delete);
      
      foreach(dataType in datas)
      {
        def instance = Activator.CreateInstance(dataType, array[], array[]) :> NWebDataInstance;
        
        foreach((f, _) in instance.Scripts) 
        {
          Scripts.Add(f);
          
          def filename = Path.GetFileName(f);
          def scriptContents = readResource(filename, dataType.Assembly);          
          
          File.WriteAllText(getAbsolutePath(filename), scriptContents);
          _ = combinedScripts.Append(scriptContents);
        }
          
        foreach((id, path) in instance.Templates) 
        {
          def filename = Path.GetFileName(path);
          def templateContents = readResource(filename, dataType.Assembly);
          Templates.Add((id, templateContents));
          File.WriteAllText(getAbsolutePath(filename), templateContents);
        }
        
        foreach(fields in instance.Fields)
        {
          _ = staticFields.Append(fields);
          _ = combinedScripts.Append(fields);
        }        
      }

      using(sha = SHA1CryptoServiceProvider()) 
      {
        def staticFieldsContent = staticFields.ToString();
        def hash = BitConverter.ToString(sha.ComputeHash(Encoding.Unicode.GetBytes(staticFieldsContent))).Replace("-", "");
        def fname = "_N_static_fields_" + hash + ".js";
        File.WriteAllText(getAbsolutePath(fname), staticFieldsContent);
        Scripts.Add(getRelativePath(fname));
        
        def combinedScriptsContent = combinedScripts.ToString();
        def hash = BitConverter.ToString(sha.ComputeHash(Encoding.Unicode.GetBytes(combinedScriptsContent))).Replace("-", "");
        def fname = "nweb-combined-" + hash + ".js";
        File.WriteAllText(getAbsolutePath(fname), combinedScriptsContent);
        CombinedScriptsFilename = getRelativePath(fname);
      }
      
      def scripts =
      [
        "NemerleWeb.Scripts.json2.min.js",
        //"NemerleWeb.Scripts.jquery.signalR-1.2.0.min.js",
        "NemerleWeb.Scripts.linq.min.js",
        "NemerleWeb.Scripts.nweb.js",
      ];
              
      try
      {
        def sb = System.Text.StringBuilder();
        foreach(file in scripts)
        {
          using(stream = typeof(NemerleWeb.JSAst).Assembly.GetManifestResourceStream(file))
          {
            if(stream != null) 
            {
              using(reader = System.IO.StreamReader(stream))
              {
                _ = sb.Append(reader.ReadToEnd());
                _ = sb.AppendLine(";");
              }
            }
            else
            {
              def errorMessage = $"!!!ERROR!!! Cannot find $file!!!";
              _ = sb.Append($"\r\n// $errorMessage\r\nconsole.error($errorMessage);\r\n");
            }
          }     
          
          File.WriteAllText(getAbsolutePath("resource.js"), sb.ToString());          
        }
      }
      catch
      {
      | e => File.WriteAllText(getAbsolutePath("resource.js"), e.ToString());
      }
      
      ResourcesFilename = getRelativePath("resource.js");
    }         
    
    public GetScripts() : IEnumerable[string]
    {
      /*
      Scripts.Select(filename => {
$<#
<script type="text/javascript" src="$filename"></script> #>
      });
      */
      [$<#
<script type="text/javascript" src="$CombinedScriptsFilename"></script> #>]
      
    }
    
    public GetTemplates() : IEnumerable[string]
    {
      Templates.Select(((id, content), _) => {
$<#
<script type="text/html" id="$(id)">
$content
</script>#>
      });
    }
    
    public GetNemerleWebScripts() : string
    {
      $<#
<script type="text/javascript" src="$ResourcesFilename"></script> #>
    }
  }
  
  public class NWebDataInstance
  {
    public Scripts : List[(string * int)] = List();
    public Templates : List[(string * string)] = List();
    public Fields : List[string] = List();
    
    public AddScript(filename : string, ancestorCount : int) : void
    {
      Scripts.Add((filename, ancestorCount));
    }

    public AddFields(fields : string) : void
    {
      Fields.Add(fields);
    }
    
    public AddTemplate(id : string, filename : string) : void
    {
      Templates.Add((id, filename));
    }
  }
}
