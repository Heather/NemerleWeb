using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  public variant JSLiteral : System.IEquatable[JSLiteral]
  {
    | Number { val : double; }
    | Bool { val : bool; }
    | String { val : string; }
    | Regexp { pattern : JSAst; modifiers : JSAst; }
    | NaN
    | Infinity
    | Null
    | Undefined
    
    [OverrideObjectEquals]
    public Equals (lit : JSLiteral) : bool
      implements System.IEquatable[JSLiteral].Equals
    {
      match ((this, lit))
      {
      | (Null, Null) => true
      | (String (x1), String (x2)) => x1 == x2
      | (Bool (x1), Bool (x2)) => x1 == x2
      | (Number (val), Number (val')) => val == val'    
      | (NaN, NaN) => true
      | (Infinity, Infinity) => true
      | (Undefined, Undefined) => true
      | _ => false      
      }
    }
  }
}
