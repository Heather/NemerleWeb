using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;


namespace Nemerle.NemerleWeb
{
    public module TyperHelper
    {
        static mutable _index : int = 0;
        
        public GetSplicesAst(expr : PExpr, typer : Typer) : IEnumerable[JsAST.DefValue] {            
            def method = BuildMethod(expr, typer);
            def oldBody = method.Header.Body;
            def defs = JsASTBuilder.BuildJsASTForSplices(GetTypedBody(method, typer, typer.CurrentTypeBuilder, false));
            method.Header.body = oldBody;
            def defs2 = JsASTBuilder.BuildJsASTForSplices(GetTypedBody(method, typer, typer.CurrentTypeBuilder));            
            method.Header.body = oldBody;
            def a = defs.Append(defs2)
                        .GroupBy(d => match(d) { | JsAST.DefValue(name, _) => name });
            a.SelectMany(x => match(x.NToList()) {
                              | JsAST.DefValue(_, Void) :: s :: [] => [s]
                              | f :: _ :: [] => [f]
                              | _ => []
                              });
        }
        
        public GetMethodJsAST(method : MethodBuilder, typer : Typer, tb : TypeBuilder) : JsAST 
        {   
            JsASTBuilder.BuildJsAST(GetTypedBody(method, typer, tb), !(method.Header.ReturnType is FixedType.Void()) && !method.IsConstructor);            
        }
        
        GetTypedBody(method : MethodBuilder, typer : Typer, tb : TypeBuilder, fullTyping : bool = true) : TExpr {
            match(method.Header.Body) {
            | FunBody.Typed(typedBody) => typedBody
            | FunBody.Parsed => 
                def oldEmitDebug = typer.Manager.Options.EmitDebug;
                typer.Manager.Options.EmitDebug = false;
                def methodTyper = Typer(tb, null, method, method.Env);
                if(fullTyping) {
                    methodTyper.RunFullTyping();
                } else {
                    def runTyper = methodTyper.GetType().GetMethod("RunTyper", BindingFlags.NonPublic | BindingFlags.Instance);
                    _ = runTyper.Invoke(methodTyper, array[]);
                }
                typer.Manager.Options.EmitDebug = oldEmitDebug;
                GetTypedBody(method, typer, tb);
            | _ => 
                Message.Error("Couldn't type body"); 
                TExpr.Error();
            }
        }
        
        BuildMethod(expr : PExpr, typer : Typer) : MethodBuilder
        {
            def tb = typer.CurrentType;
            def methodName = "__TyperHelperMethod" + _index;
            _index++;
            def meth = tb.DefineAndReturn(<[ decl: $(methodName : dyn)() : void { $expr } ]>) :> MethodBuilder;
            tb.Compile();
            meth
        }
    }
}
