using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;
using NemerleWeb.Utils;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  /// <summary>
  /// Description of JsKnownMembersSCGList.
  /// </summary>
  module JsKnownMembersSCGList
  {
    public Register() : void
    {
      JsKnownMembers.RegisterType(typeof(List[_]), Handler); 
    }
    
    private Handler(left : JsAst, member : IMember, parms : list[JsAst]) : option[JsAst]
    {
      if (member.Name == ".ctor")
        Some(JsAst.Array(parms.Filter(p => !(p is JsAst.NotImpl()))))
      else      
        match (parms)
        {
        | [] =>
          match (member.Name)
          {
          | "get_Count" => Some(JsAst.FieldRef(left, "length", true))
          | "GetEnumerator" => Some(left.MethodCall(member.Name, parms))
          | _ => None()
          }
        
        | [_] =>
          match (member.Name)
          {
          | "get_Item" => Some(JsAst.Call(JsAst.Indexer(left), parms))
          | "Add" => Some(JsAst.Call(JsAst.MethodRef(TypeHelper.PropertyToField(left, false), "push"), parms))
          | "Remove" => Some(JsAst.Call(JsAst.MethodRef(TypeHelper.PropertyToField(left, false), "remove"), parms))
          | _ => None()
          }
        
        | _ => None()
        }
    }
  }
}
