using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  /// <summary>
  /// Description of JSKnownMembersSCGList.
  /// </summary>
  module JSKnownMembersSCGList
  {
    public Register() : void
    {
      JSKnownMembers.RegisterType(typeof(List[_]), Handler); 
    }
    
    private Handler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      (if (left is JSAst.StaticMethodRef) StaticHandler else InstanceHandler)(left, member, parms)
    }
    
    private InstanceHandler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [] =>
        match (member.Name)
        {
        | "get_Count" => Some(JSAst.FieldRef(left, "length"))
        | "Clear" =>
          Some(JSAst.Assign(JSAst.FieldRef(left, "length"), JSAst.Literal(JSLiteral.Number(0))))
        | "GetEnumerator" => Some(left.JSMethodCall(member.Name, parms))
        | "ToArray" => Some(left)
        | _ => None()
        }
        
      | [arg] =>
        match (member.Name)
        {
        | "get_Item" => Some(JSAst.ArrayIndexer(left, arg))
        | "Add" => Some(left.JSMethodCall("push", parms))
        | "Remove" => Some(left.JSMethodCall("remove", parms))
        | _ => None()
        }      
      | items => 
        match(member.Name, items)
        {
        | ("Insert", [arg1, arg2]) => Some(left.JSMethodCall("splice", [arg1, JSAst.Literal(JSLiteral.Number(0)), arg2]))
        | ("set_Item", [arg1, arg2]) => Some(JSAst.Assign(JSAst.ArrayIndexer(left, arg1), arg2))
        | ("Add", _) => Some(left.JSMethodCall("push", [JSAst.Tuple(parms)]))
        | _ => None()
        }
      }
    }
     
    private StaticHandler(_left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [] =>
        match (member.Name)
        {
        | ".ctor" => Some(JSAst.Array([]))
        | _ => None()
        }
      | [arg] =>
        match (member.Name)
        {
        | ".ctor" =>
          Some(JSAst.Call(JSAst.GlobalRef("System_Collections_Generic_List"), [arg]))
        | _ => None()
        }
      | _ => None()
      }
    }
  }
}
