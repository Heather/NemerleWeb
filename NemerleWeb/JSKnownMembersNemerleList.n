using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  module JSKnownMembersNemerleList
  {
    public Register() : void
    {
      JSKnownMembers.RegisterType(typeof(list[_]), Handler);
      JSKnownMembers.RegisterType(typeof(list[_].Cons), Handler);
      JSKnownMembers.RegisterType(typeof(list[_].Nil), Handler);
    }
    
    private Handler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      (if (left is JSAst.StaticMethodRef) StaticHandler else InstanceHandler)(left, member, parms)
    }
    
    private InstanceHandler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [] =>
        match (member.Name)
        {
        | "get_Length" => Some(JSAst.FieldRef(left, "length"))
        | _ => None()
        }
      | [arg1] =>
        match (member.Name)
        {        
        | "Equals" =>
          Some(
            JSAst.Call(
              JSAst.StaticMethodRef("nweb.collection", "areArrayEqual", None()),
              [left, arg1]))        
        | "MapToArray" => 
          Some(left.AsJSEnumerable().JSMethodCall("select", [arg1]).JSMethodCall("toArray", []))
        | _ => None()
        }
      | _ => None()
      }
    }
    
    private StaticHandler(_left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match(parms)
      {
      | [arg1, arg2] =>
        match(member.Name)
        {
        | ".ctor" =>
          // Optimize lists
          match((arg1, arg2))
          {
          | (JSAst.Array(a1), JSAst.Array(a2)) => Some(JSAst.Array(a1 + a2))
          | (JSAst.Array(a1), _) => Some(JSAst.Array(a1 + [arg2]))
          | (_, JSAst.Array(a2)) => Some(JSAst.Array(arg1 :: a2))          
          | _ => Some(JSAst.Array([arg1]).JSMethodCall("concat", [arg2]))
          }
        | _ => None()
        }
      | _ => None()
      }
    }
  }
}