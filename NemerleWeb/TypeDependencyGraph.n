using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  public class TypeDependencyGraph
  {
    public _dependencyMap = Dictionary.[string, List[string]]();
    
    public Add([NotNull] typeName : string, [NotNull] tp : FixedType) : void
    { 
      when(tp.TypeInfo != null)
        Add(typeName, tp.TypeInfo);
    }
        
    public Add([NotNull] typeName : string, [NotNull] ti : TypeInfo) : void
    {
      //If 'ti' is variant option, then use variant itself
      // ti = if(ti.VariantOptionCode > -1) ti.BaseType else ti;
      
      def hasUnitInHierarchy(ti) {        
        def typeWithRootExists(root) {          
          ti.GlobalEnv.LookupType($"$root.$(ti.FullName)".Split('.').NToList(), 
                                  null, ti.TyparmsCount) is Some(_)
        }
       
        if(ti.IsCustomAttributeDefined("NemerleWeb.UnitAttribute")) true
        else if(ti.DeclaringType != null) hasUnitInHierarchy(ti.DeclaringType)
        else JSClassSettings.JSClrRoots.Any(typeWithRootExists)
      }
      
      def addDependency(ti : TypeInfo) : void
      {
        def dependentTypeName = ti.FullName;
        mutable types;
        
        if(_dependencyMap.TryGetValue(typeName, out types))
        {
          when(!types.Contains(dependentTypeName))
            types.Add(dependentTypeName);
        }
        else
        {
          types = List();
          types.Add(dependentTypeName);
          _dependencyMap.Add(typeName, types);
        }
      }
      
      // Add only Unit classes, and do not add myself.
      when(!ti.IsCustomAttributeDefined("NemerleWeb.IgnoreAttribute") && hasUnitInHierarchy(ti) && typeName != ti.FullName)
      {
        def isVariant = ti.GetTydecl() is TypeDeclaration.Variant;
        when(isVariant)
        {when(ti.FullName.Contains("Color"))assert2(false);
          // Add nested before the variant class itself
          foreach(type in JSClass.GetNestedTypes(ti))
          {
            addDependency(type);
          }
        }
      
        // Add the type
        addDependency(ti);
      }
    }
    
    public GetTypeDependencies(typeName : string) : IEnumerable[string]
    {
      mutable lst;
      if(_dependencyMap.TryGetValue(typeName, out lst)) lst
      else []
    }
  }
}
