using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;

using System;
using System.Collections.Generic;
using System.Linq;

namespace TSParser
{
  [PegGrammar(Options = EmitDebugSources,
      start,
      grammar
      {
        // We create void rules to write less code in handler and even skip writing handler.
        #region Literals
        
        Spacing : void = WhiteSpace / LineTerminatorSequence / Comment;
        s : void = Spacing*;
        S : void = Spacing+;
        
        // String ending, this is not defined in specification, but real TS parser works that way.
        // Maybe think about better rule.
        // Using this rule it parses "a /**/ b /**/" while it must not.
        endS : void = s semicolon? s;
        
        // GlobalSpacing is used for finding references
        GlobalSpacing : void = WhiteSpace / LineTerminatorSequence / GlobalComment;
        GS : void = (GlobalSpacing / semicolon)*;
        
        import_s : void   = "import"s;
        export_s : void   = "export"s;
        module_s : void   = "module"s;
        declare_s : void  = "declare"s;
        var_s : void      = "var"s;
        function_s : void = "function"s;
        new_s : void      = "new"s;
        delete_s : void = "delete"s;
        void_s : void = "void"s;
        typeof_S : void = "typeof"S;
        constructor_s : void = "constructor"s;
        enum_s : void = "enum"s;
        class_s : void = "class"s;
        implements_s : void = "implements"s;
        extends_s : void = "extends"s;
        get_s : void = "get"s;
        set_s : void = "set"s;
        interface_s : void = "interface"s;
        require_s : void = "require"s;
        
        dot : void = '.';
        dot_s : void = dot s;
        comma : void = ',';
        comma_s : void = comma s;
        colon : void = ':';
        colon_s : void = colon s;
        semicolon : void = ';';
        semicolon_s : void = semicolon s;
        lbrace : void = '{';
        lbrace_s : void = lbrace s;
        rbrace : void = '}';
        rbrace_s : void = rbrace s;
        lbrack : void = '[';
        lbrack_s : void = lbrack s;
        rbrack : void = ']';
        rbrack_s : void = rbrack s;
        lparen : void = '(';
        lparen_s : void = lparen s;
        rparen : void = ')';
        less : void = '<';
        less_s : void = less s;
        greater : void = '>';
        greater_s : void = greater s;
        rparen_s : void = rparen s;
        assign : void = '=';
        assign_s : void = assign s;
        squote : void = '\'';
        squote_s : void = squote s;
        dquote : void = '"';
        dquote_s : void = dquote s;
        
        ellipsis : void = "...";
        ellipsis_s : void = ellipsis s;
        
        arrow : void = "=>";
        arrow_s : void = arrow s;
        
        #endregion
        
        #region Special rules
        
        required : void = !'?';
        optional : bool = ('?' s)?;
        static_ : bool = "static";
        static_s : bool = static_ s;
        
        #endregion
        
        #region EcmaScript
        
        #region SourceCharacter (6)

        SourceCharacter = [Any];

        #endregion

        #region WhiteSpace (7.2)

        NBSP = '\u00A0';
        BOM = '\uFEFF';
        WhiteSpace = [Zs] / '\t' / '\v' / '\f' / ' ' / NBSP / BOM;        

        #endregion

        #region Line terminators (7.3)

        LineSeparator = '\u2028';
        ParagraphSeparator = '\u2029';
        LineTerminator = '\n' / '\r' / LineSeparator / ParagraphSeparator;
        LineTerminatorSequence = "\r\n" / LineTerminator;

        #endregion

        #region Comment (7.4)
        
        Comment = MultiLineComment / SingleLineComment;
        MultiLineComment = "/*" (!"*/" [Any])* "*/";
        SingleLineComment = "//" (!LineTerminator [Any])*;
        
        // Support for /// <reference
        GlobalComment = MultiLineComment / GlobalSingleLineComment;
        GlobalSingleLineComment = "//" !('/'s ReferenceElement s) (!LineTerminator [Any])*;

        #endregion
        
        #region Identifier (7.6)
        
        ZWNJ = '\u200C';
        ZWJ = '\u200D';        
        
        // Any name
        AnyIdentifier : string = IdentifierName s;
        Identifier : string = !(ReservedWord Spacing+) IdentifierName s;
        IdentifierName : string = IdentifierStart+ IdentifierPart*;
        IdentifierStart = UnicodeLetter / '$' / '_' / '\\' UnicodeEscapeSequence;
        IdentifierPart =
          IdentifierStart /
          UnicodeCombiningMark /
          UnicodeDigit /
          UnicodeConnectorPunctuation /
          ZWNJ /
          ZWJ;
          
        UnicodeLetter = [Lu, Ll, Lt, Lm, Lo, Nl];
        UnicodeCombiningMark = [Mn, Mc];
        UnicodeDigit = [Nd];
        UnicodeConnectorPunctuation = [Pc];        
        
        #endregion
        
        #region ReservedWord (7.6.1)
        
        ReservedWord = Keyword / FutureReservedWord / StrictReservedWord / NullLiteral / BooleanLiteral;
        StrictModeReservedWord = ReservedWord / StrictReservedWord;
        
        Keyword =
          "break" / "do" / "instanceof" / "typeof" / "case" / "else" / 
          "new" / "var" / "catch" / "finally" / "return" / "void" / "continue" /
          "for" / "switch" / "while" / "debugger" / "function" / "this" / "with" / 
          "default" / "if" / "throw" / "delete" / "in" / "try";
          
        // 7.6.1.2          
        FutureReservedWord =
          "class" / "enum" / "extends" / "super" / "const" / "export" / "import";
          
        // 10.1.1  
        StrictReservedWord =
          "implements" / "let" / "private" / "public" / "yield" / "interface" /
          "package" / "protected" / "static";
        
        #endregion
        
        #region Literals (7.8)

        Literal = NullLiteral / BooleanLiteral / NumericLiteral / StringLiteral / RegularExpressionLiteral;

        #endregion
        
        #region NullLiteral (7.8.1)
        
        NullLiteral = "null";
        
        #endregion
        
        #region BooleanLiteral (7.8.2)
        
        BooleanLiteral = "true" / "false";
        
        #endregion

        #region NumericLiteral (7.8.3)

        NumericLiteral = (DecimalLiteral / HexIntegerLiteral) !(IdentifierStart / DecimalDigit);

        DecimalLiteral =
          DecimalIntegerLiteral dot DecimalDigit* ExponentPart? /
          dot DecimalDigit+ ExponentPart? /
          DecimalIntegerLiteral ExponentPart?;

        DecimalIntegerLiteral = '0' / NonZeroDigit DecimalDigit*;
        DecimalDigit = ['0'..'9'];
        NonZeroDigit = ['1'..'9'];        
        ExponentPart = ExponentIndicator SignedInteger;
        ExponentIndicator = 'e' / 'E';
        SignedInteger = DecimalDigit+ / '+' DecimalDigit+ / '-' DecimalDigit+;
        HexIntegerLiteral = ("0x" / "0X") HexDigit+;
        HexDigit = ['0'..'9', 'a'..'f', 'A'..'F'];        

        #endregion

        #region StringLiteral (7.8.4)
        
        StringLiteral : string =
          dquote DoubleStringCharacter* dquote /
          squote SingleStringCharacter* squote;
        DoubleStringCharacter = 
          !('"' / '\\' / LineTerminator) SourceCharacter /
          '\\' EscapeSequence /
          LineContinuation;                 
        SingleStringCharacter =
          !('\'' / '\\' / LineTerminator) SourceCharacter /
          '\\' EscapeSequence /
          LineContinuation;
        LineContinuation = '\\' LineTerminatorSequence ;
        
        EscapeSequence = 
          CharacterEscapeSequence /
          '0' !DecimalDigit /
          HexEscapeSequence /
          UnicodeEscapeSequence;
        
        CharacterEscapeSequence = SingleEscapeCharacter / NonEscapeCharacter;
        SingleEscapeCharacter = '\'' / '\"' / '\\' / 'b' / 'f' / 'n' / 'r' / 't' / 'v';
        NonEscapeCharacter = !(EscapeCharacter / LineTerminator) SourceCharacter;        
        EscapeCharacter = SingleEscapeCharacter / DecimalDigit / 'x' / 'u';
        HexEscapeSequence = 'x' HexDigit HexDigit;
        UnicodeEscapeSequence = 'u' HexDigit HexDigit HexDigit HexDigit;
        
        #endregion
        
        #region Regular Expression Literals (7.8.5)
        
        RegularExpressionLiteral = '/'s RegularExpressionBody '/'s RegularExpressionFlags s;
        RegularExpressionBody = RegularExpressionFirstChar RegularExpressionChars;
        RegularExpressionChars = RegularExpressionChar*;
        RegularExpressionFirstChar =
          !'*' !'\\' !'/' !'[' RegularExpressionNonTerminator /
          RegularExpressionBackslashSequence /
          RegularExpressionClass;
        RegularExpressionChar =
          !'\\' !'/' !'[' RegularExpressionNonTerminator /
          RegularExpressionBackslashSequence /
          RegularExpressionClass;
        RegularExpressionBackslashSequence = '\\'s RegularExpressionNonTerminator;
        RegularExpressionNonTerminator = !LineTerminator SourceCharacter;
        RegularExpressionClass = lbrack_s RegularExpressionClassChars rbrack_s;
        RegularExpressionClassChars = RegularExpressionClassChar*;
        RegularExpressionClassChar = 
          !']' !'\\' RegularExpressionNonTerminator /
          RegularExpressionBackslashSequence;        
        RegularExpressionFlags = IdentifierPart?;
        
        #endregion
        
        #region Primary Expressions (11.1)
        
        PrimaryExpression =
          "this" /
          Identifier /
          Literal /
          ArrayLiteral /
          ObjectLiteral /
          lparen_s Expression rparen_s;
          
        #endregion
        
        #region Array Initialiser (11.1.4)
        
        ArrayLiteral = lbrack_s (ElementList / ElementList comma_s Elision? / Elision?) rbrack_s;
        ElementList = Elision? AssignmentExpression (comma_s Elision? AssignmentExpression)*;
        Elision = (comma_s)+;
        
        #endregion
        
        #region Object Initialiser (11.1.5)

        ObjectLiteral =
          lbrace_s rbrace_s /
          lbrace_s PropertyNameAndValueList rbrace_s /
          lbrace_s PropertyNameAndValueList comma_s rbrace_s;
        PropertyNameAndValueList = PropertyAssignment (comma_s PropertyAssignment)*;
        
        // Modified in TypeScript (4.5)
        //PropertyAssignment =
        //  PropertyName colon_s AssignmentExpression /
        //  get_s PropertyName lparen_s rparen_s lbrace_s FunctionBody rbrace_s /
        //  set_s PropertyName lparen_s PropertySetParameterList rparen_s lbrace_s FunctionBody rbrace_s;
          
        PropertyName = (IdentifierName / StringLiteral / NumericLiteral) s;
        PropertySetParameterList = Identifier;
        
        #endregion
        
        #region Left-Hand-Side Expressions (11.2)
        
        MemberExpression =
          (new_s)? (PrimaryExpression / FunctionExpression) (lbrack_s Expression rbrack_s / dot_s IdentifierName)+ Arguments?;
        NewExpression = MemberExpression / new_s NewExpression;
        CallExpression = MemberExpression (Arguments+ / lbrack_s Expression rbrack_s / dot_s IdentifierName);
        Arguments = lparen_s ArgumentList? rparen_s;
        ArgumentList = AssignmentExpression (comma_s AssignmentExpression)*;

        LeftHandSideExpression = NewExpression / CallExpression;
        
        #endregion
        
        #region Postfix Expression
        
        PostfixExpression =
          LeftHandSideExpression /
          LeftHandSideExpression !LineTerminator "++" /
          LeftHandSideExpression !LineTerminator "--";
        
        #endregion
        
        #region Unary Operators (11.4)
        
        UnaryExpression =
          PostfixExpression /
          "delete"s UnaryExpression /
          "void"s UnaryExpression /
          "typeof"s UnaryExpression /
          "++"s UnaryExpression /
          "--"s UnaryExpression /
          '+'s UnaryExpression /
          '-'s UnaryExpression /
          '~'s UnaryExpression /
          '!'s UnaryExpression;
        
        #endregion
        
        #region Multiplicative Operators (11.5)
        
        MultiplicativeExpression = UnaryExpression (('*' / '/' / '%')s UnaryExpression)*;
        
        #endregion
        
        #region Additive Operators (11.6)
        
        AdditiveExpression = MultiplicativeExpression (('+' / '-')s MultiplicativeExpression)*;
        
        #endregion
        
        #region Bitwise Shift Operators (11.7)
        
        ShiftExpression = AdditiveExpression (("<<" / ">>" / ">>>")s AdditiveExpression)*;
        
        #endregion
        
        #region Relational Operators (11.8)
        
        RelationalExpression =
          ShiftExpression (('<' / '>' / "<=" / ">=" / "instanceof" / "in")s ShiftExpression)*;
        
        #endregion
        
        #region Equality Operators (11.9)
        
        EqualityExpression = RelationalExpression (("==" / "!=" / "===" / "!==")s RelationalExpression)*;
        
        #endregion
        
        #region Binary Bitwise Operators (11.10)
        
        BitwiseANDExpression = EqualityExpression ('&'s EqualityExpression)*;        
        BitwiseXORExpression = BitwiseANDExpression ('^'s BitwiseANDExpression)*;   
        BitwiseORExpression = BitwiseXORExpression ('|'s BitwiseXORExpression)*;
        
        #endregion
        
        #region Binary Logical Expression (11.11)
        
        LogicalANDExpression = BitwiseORExpression ("&&"s BitwiseORExpression)*;        
        LogicalORExpression = LogicalANDExpression ("||"s LogicalANDExpression)*;
        
        #endregion
        
        #region Conditional Operator (11.12)
        
        ConditionalExpression =
          LogicalORExpression /
          LogicalORExpression '?'s AssignmentExpression ':'s AssignmentExpression;
        
        #endregion
        
        #region Assignement Operators (11.13)
        
        AssignmentExpression =
          ConditionalExpression /
          LeftHandSideExpression assign AssignmentExpression /
          LeftHandSideExpression AssignmentOperator AssignmentExpression;
        
        AssignmentOperator = "*=" / "/=" / "%=" / "+=" / "-=" / "<<=" / ">>=" / ">>>=" / "&=" / "^=" / "|=";
        
        #endregion
        
        #region Comma Operator (11.14)
        
        Expression = AssignmentExpression (comma_s AssignmentExpression)*;
        
        #endregion

        #region Statement (12)
        
        // TODO: Add other statements
        Statement = VariableStatement;
        
        #endregion
        
        #region Variable Statement (12.2)
        
        VariableStatement = var_s VariableDeclarationList endS;
        VariableDeclarationList = VariableDeclaration (comma_s VariableDeclaration)*;
        // This rule is modified in TypeSript see: 5.1
        //VariableDeclaration = Identifier;
        Initialiser /*: string*/ = assign AssignmentExpression;
        
        #endregion
        
        #region Function Definition (13)
        
        FunctionExpression = function_s Identifier? lparen_s FormalParameterListOpt rparen_s lbrace_s FunctionBody rbrace_s;
        FormalParameterList = Identifier (comma_s Identifier)*;
        FormalParameterListOpt = FormalParameterList?;
        FunctionBody = SourceElement*;
        
        #endregion
        
        #region Program (14)
        
        SourceElement = Statement / FunctionDeclaration;
        
        #endregion
        
        #endregion
        
        #region TypeScript
        
        #region Type Parameter Lists (3.5.1)
        
        TypeParameters : List[ScriptTypeParameter] = less_s TypeParameterList greater_s;
        // Return list instead of option
        TypeParametersOpt : List[ScriptTypeParameter] = TypeParameters?;
        TypeParameterList : List[ScriptTypeParameter] = TypeParameter (comma_s TypeParameter)*;
        TypeParameter : ScriptTypeParameter = Identifier Contraint?;
        // ObjectType is not specified but it is allowed by compiler
        Contraint : ScriptType = extends_s (TypeReference / ObjectType);
        
        #endregion
        
        #region Specifying Types (3.6)
        
        // Array is specified here, because otherwise we have left recursion.
        Type : ScriptType = PredefinedType / TypeQuery / TypeReference / TypeLiteral;
        ScriptType : ScriptType = Type ('['s ']'s)*;
        
        // Note: bool is not a valid type in TypeScript 0.9 but many libraries still use it.
        PredefinedType : ScriptType = ("any" / "number" / "bool" / "boolean" / "string" / "void") s !IdentifierPart;
        
        TypeReference : ScriptType = TypeName TypeArguments?;
        TypeName : string = Identifier (dot_s Identifier)*;        
        ModuleName : string = TypeName;
        
        TypeArguments : List[ScriptType] = less_s TypeArgumentList greater_s;
        TypeArgumentList : List[ScriptType] = TypeArgument (comma_s TypeArgument)*;
        TypeArgument : ScriptType = ScriptType;        
        
        #endregion
        
        #region Type Queries
        
        TypeQuery : ScriptType = typeof_S TypeQueryExpression;
        TypeQueryExpression : string = Identifier (dot_s IdentifierName)*;
        
        #endregion
        
        #region Type Literals (3.6.4)
        
        // Array is excluded to prevent left recursion.
        TypeLiteral : ScriptType = ObjectType / FunctionType / ConstructorType;        
        
        ArrayType : ScriptType.Array = Type ('['s ']'s)+;
        FunctionType : ScriptType = lparen_s ParameterListOpt rparen_s arrow_s ScriptType s;
        ConstructorType : ScriptType = new_s lparen_s ParameterListOpt rparen_s arrow_s ScriptType s;
        
        #endregion
                 
        #region Object Type Literals (3.7)
        
        ObjectType : ScriptType = ObjectTypeStrict;
        ObjectTypeStrict : ScriptType.Object = lbrace_s TypeBody? rbrace_s;
        TypeBody /*: List[TSObject]*/ = TypeMemberList semicolon? endS;
        TypeMemberList /*: List[TSObject]*/ = TypeMember (endS TypeMember)* endS;
        TypeMember /*: TSObject*/ =
          CallSignature /
          ConstructSignature /
          MethodSignature /          
          IndexSignature /
          PropertySignature;
          
        #endregion
          
        #region Property Signatures (3.7.1)

        PropertySignature /*: TSObject*/ = PropertyName optional TypeAnnotation?;
        
        #endregion
        
        #region Call Signatures (3.7.2)
        
        CallSignature =
          TypeParametersOpt lparen_s ParameterListOpt rparen_s TypeAnnotation?;
          
        #endregion
        
        #region Parameter List (3.7.2.2)
        
        // According to spec, additional comma is forbiden, but TS compiler handles it.
        ParameterList /*: List[TSParam]*/ =
          ParameterListROR /
          ParameterListRR /
          ParameterListRO /
          ParameterListOR /          
          RequiredParameterList /
          OptionalParameterList /
          RestParameter;
          
        // Return empty list instead of option
        ParameterListOpt /*: List[TSParam]*/ = ParameterList?;
        
        ParameterListROR /*: List[TSParam]*/ =
          RequiredParameterList comma_s OptionalParameterList comma_s RestParameter;
        ParameterListOR  /*: List[TSParam]*/ =
          OptionalParameterList comma_s RestParameter;
        ParameterListRO  /*: List[TSParam]*/ =
          RequiredParameterList comma_s OptionalParameterList;
        ParameterListRR  /*: List[TSParam]*/ =
          RequiredParameterList comma_s RestParameter;
          
        RequiredParameterList /*: List[TSParam]*/ = RequiredParameter (comma_s RequiredParameter)*;
        RequiredParameter /*: TSParam*/ =
          Identifier colon_s StringLiteral /
          PublicOrPrivate? Identifier required TypeAnnotation?;
          
        Public /*: MemberAccess*/ = "public";
        Private /*: MemberAccess*/ = "private";
        PublicOrPrivate /*: MemberAccess*/ = (Public/Private)s;
        OptionalParameterList /*: List[TSParam]*/ = OptionalParameter (comma_s OptionalParameter)*;
        // Optional parameter must not have Initialiser in declaration files.
        OptionalParameter /*: TSParam*/ = PublicOrPrivate? Identifier optional TypeAnnotation?;
        RestParameter /*: List[TSParam]*/ = ellipsis_s Identifier TypeAnnotation?;
        
        #endregion
        
        #region Construct/Index/Method Signatures (3.7.3, 3.7.4, 3.7.5)
        
        ConstructSignature /*: TSObject*/ =
          new_s TypeParametersOpt lparen_s ParameterListOpt rparen_s TypeAnnotation?;
        IndexSignature     /*: TSObject*/ =
          lbrack_s Identifier required colon_s ("number" / "string") s rbrack_s TypeAnnotation;
        MethodSignature  /*: TSObject*/ = PropertyName optional CallSignature;
        
        #endregion
        
        #region Object Literals (4.5)
        
        PropertyAssignment =
          PropertyName colon_s AssignmentExpression /
          PropertyName CallSignature lbrace_s FunctionBody rbrace_s /
          GetAccessor /
          SetAccessor;
          
        GetAccessor =
          get_s PropertyName lbrack_s rbrack_s TypeAnnotation? lbrace_s FunctionBody rbrace_s;
        SetAccessor =
          set_s PropertyName lbrack_s Identifier TypeAnnotation? rbrack_s;
        
        #endregion
        
        #region Variable Statements (5.1)
        
        VariableDeclaration = Identifier TypeAnnotation?;
        TypeAnnotation : ScriptType = colon_s ScriptType s;
        
        #endregion
        
        #region Function Declarations (6.1)
        
        // Modified
        FunctionDeclaration = FunctionOverload+;
        FunctionOverload = function_s Identifier CallSignature endS;
        
        #endregion       
        
        #region Interface Declarations (7.1)
        
        InterfaceDeclaration : DeclarationElement =
          interface_s Identifier TypeParametersOpt InterfaceExtendsClause? ObjectTypeStrict;
        InterfaceExtendsClause : List[ScriptType] = extends_s ClassOrInterfaceTypeList;
        ClassOrInterfaceTypeList : List[ScriptType] =
          ClassOrInterfaceType (comma_s ClassOrInterfaceType)*;
        ClassOrInterfaceType : ScriptType = TypeReference;
        
        #endregion
        
        #region Class Declarations (8.1)
        
        ClassDeclaration =
          class_s Identifier TypeParametersOpt ClassHeritage lbrace_s ClassBody rbrace_s;
        
        ClassHeritage /*: ClassHeritage*/ = ClassExtendsClause? ImplementsClause?;
        ClassExtendsClause /*: string*/ = extends_s ClassType;
        ClassType = TypeReference;
        ImplementsClause /*: List[string]*/ = implements_s ClassOrInterfaceTypeList;
        
        ClassBody = ClassElement*;
        ClassElement =
          ConstructorDeclaration /
          MemberDeclaration /
          IndexSignature;
        
        #endregion
        
        #region Constructor Declaration (8.3)
        
        ConstructorDeclaration = ConstructorOverload+;
        ConstructorOverload = constructor_s lparen_s ParameterListOpt rparen_s endS;
        
        #endregion
        
        #region Member Declarations (8.4)
        
        MemberDeclaration =
          MemberFunctionDeclaration /
          MemberVariableDeclaration /
          MemberAccessorDeclaration;
        
        MemberVariableDeclaration =
          PublicOrPrivate? static_s? PropertyName TypeAnnotation? endS;
        
        MemberFunctionDeclaration = MemberFunctionOverload+;
        MemberFunctionOverload = PublicOrPrivate? static_s? PropertyName CallSignature endS;
        
        MemberAccessorDeclaration = PublicOrPrivate? static_s? (GetAccessor / SetAccessor);
        
        #endregion
        
        #region Enums (9)
        
        EnumDeclaration = enum_s Identifier lbrace_s EnumBody? rbrace_s endS;
        EnumBody /*: List[TSEnumElement]*/ =
          ConstantEnumMember (comma_s ConstantEnumMember)* comma_s?;
        ConstantEnumMember = PropertyName (assign_s IntegerLiteral)?;
        IntegerLiteral = SignedInteger / HexIntegerLiteral;
        ComputedEnumMemberList = ComputedEnumMember (comma_s ComputedEnumMember)*;
        ComputedEnumMember = PropertyName assign_s AssignmentExpression;
        
        #endregion
        
        #region Module Declarations (10.1)
        
        ModuleDeclaration = module_s IdentifierPath lbrace_s GS ModuleBody rbrace_s;
        IdentifierPath : string = Identifier (dot_s Identifier)*;        
        
        #endregion
        
        #region Module Body (10.2)
        
        ModuleBody = ModuleElement*;
        
        ModuleElement =
          Statement /
          export_s?
          (
            FunctionDeclaration /
            ClassDeclaration /
            InterfaceDeclaration /
            ModuleDeclaration /
            EnumDeclaration /
            ImportDeclaration
          ) endS;
          
        #endregion
        
        #region Import Declarations (10.3)
        
        ImportDeclaration : DeclarationElement =
          import_s Identifier assign_s (ModuleEntityName / EntityName) endS;
        EntityName : string = ModuleName (dot Identifier)?;
        
        // NOTE: This rule is not according to specification
        ModuleEntityName : string = module_s lparen_s StringLiteral rparen_s;
        
        #endregion

        #region Source Files (11)
        
        SourceFile = DeclarationSourceFile;
        DeclarationSourceFile : List[DeclarationElement] = GS (DeclarationElement endS)*;
        DeclarationElement : DeclarationElement =
          Reference /
          ExportAssignment /
          export_s? InterfaceDeclaration /
          export_s? ExternalImportDeclaration /
          export_s? ImportDeclaration/          
          export_s? AmbientDeclaration;
        
        #endregion
        
        #region External Import Declarations (11.2.2)
        
        ExternalImportDeclaration : DeclarationElement =
          import_s Identifier assign_s ExternalModuleReference endS;        
        ExternalModuleReference : string = require_s lbrack_s StringLiteral s rbrack_s;
        
        #endregion
        
        #region Export Assignments (11.2.4)
        
        // NOTE: This is not according to specification
        ExportModuleAssignment : ExportAssignmentRule = export_s Identifier assign_s ModuleEntityName endS;
        ExportIdentifierAssignment : ExportAssignmentRule = export_s assign_s Identifier endS;
        ExportAssignment : DeclarationElement = ExportModuleAssignment / ExportIdentifierAssignment;
        
        #endregion
        
        #region Ambient Declarations (12.1)
                
        AmbientDeclaration : DeclarationElement =
          declare_s
           (
            AmbientVariableDeclaration /
            AmbientFunctionDeclaration /
            AmbientClassDeclaration /
            AmbientEnumDeclaration /
            AmbientExternalModuleDeclaration /
            AmbientModuleDeclaration
           ) endS;
        
        AmbientVariableDeclaration : AmbientDeclarationRule = var_s Identifier TypeAnnotation?;
        AmbientFunctionDeclaration : AmbientDeclarationRule = function_s Identifier CallSignature;
        
        // Class
        AmbientClassDeclaration : AmbientDeclarationRule = 
          class_s Identifier TypeParametersOpt ClassHeritage
          lbrace_s AmbientClassBody rbrace_s endS;
        AmbientClassBody /*: List[TSClassMember]*/ = AmbientClassBodyElement*;
        AmbientClassBodyElement /*: TSClassMember*/ =
          (AmbientConstructorDeclaration / AmbientMemberDeclaration / IndexSignature) endS;
        AmbientConstructorDeclaration /*: TSClassMember*/ =
          constructor_s lparen_s ParameterListOpt rparen_s;
        AmbientMemberDeclaration /*: TSClassMember*/ =
          PublicOrPrivate? static_s? PropertyName (CallSignature / TypeAnnotation?);
                
        // Enum
        AmbientEnumDeclaration : AmbientDeclarationRule =
          enum_s Identifier lbrace_s AmbientEnumBody? rbrace_s;
        AmbientEnumBody = AmbientEnumMember*;
        AmbientEnumMember = PropertyName (assign_s IntegerLiteral)? comma_s?;
        
        // Module
        AmbientModuleDeclaration : AmbientDeclarationRule =
          module_s IdentifierPath lbrace_s AmbientModuleBody rbrace_s;
        AmbientModuleBody /*: List[TSGlobal]*/ = AmbientModuleElement*;
        AmbientModuleElement =
          export_s?
          (
            AmbientVariableDeclaration /
            AmbientFunctionDeclaration /
            AmbientClassDeclaration /
            InterfaceDeclaration /
            AmbientEnumDeclaration /
            AmbientModuleDeclaration /
            ImportDeclaration
          ) endS;
          
        // External
        AmbientExternalModuleDeclaration : AmbientDeclarationRule =
          module_s StringLiteral s lbrace_s s AmbientExternalModuleBody rbrace_s endS;
        AmbientExternalModuleBody = AmbientExternalModuleElement*;
        AmbientExternalModuleElement =          
          ExportAssignment /
          export_s? ExternalImportDeclaration /
          AmbientModuleElement;
          
        #endregion               
        
        #region Reference

        /// <reference path="..." />
        ReferencePath : ReferenceRule = "path"s assign_s StringLiteral;
        ReferenceNoDefaultLib : ReferenceRule =
          "no-default-lib"s assign_s ("\"true\"" / "'true'");
        ReferenceElement : DeclarationElement =
          less_s "reference"s (ReferencePath / ReferenceNoDefaultLib)s '/'s greater_s;
        xmlComment : void = "///";
        Reference : DeclarationElement = xmlComment s ReferenceElement;
        
        #endregion
        
        #endregion
        
        start : List[DeclarationElement] = DeclarationSourceFile;
      }
    )]
  public class TSDeclarationParser
  {
    #region Helpers
    
    static Id(first : string, rest : List[string]) : string
    {
      if (rest.Count == 0) first else $<#$first...$(rest; ".")#>
    }
    
    static Id(first : string, rest : option[List[string]]) : string
    {
      Id(first, rest ?? List())
    }
    
    static Id(first : string, rest : option[string]) : string
    {
      if(rest is Some(r)) $"$(first).$r" else first
    }
    
    static MakeList[T](first : T, rest : List[T]) : List[T]
    {
      def ret = List();
      ret.Add(first);
      ret.AddRange(rest);
      ret
    }
    
    #endregion
    
    optional(n : NToken) : bool
    {
      !n.IsEmpty
    }
    
    static_(n : NToken) : bool
    {
      !n.IsEmpty
    }
    
    StringLiteral(s : NToken) : string
    {
      GetText(s)
    }
    
    IdentifierName(first : NToken, rest : NToken) : string
    {
      GetText(first) + GetText(rest)
    }
    
    
    
    
    /*
    Initialiser(assignmentExpr : NToken) : string
    {
      GetText(assignmentExpr)
    }
*/
    TypeParametersOpt(l : option[List[ScriptTypeParameter]]) : List[ScriptTypeParameter]
    {
      l.WithDefault(List())
    }

    TypeParameterList(first : ScriptTypeParameter, rest : List[ScriptTypeParameter]) : List[ScriptTypeParameter]
    {
      MakeList(first, rest)
    }

    TypeParameter(id : string, constraint : option[ScriptType]) : ScriptTypeParameter
    {
      ScriptTypeParameter(id, constraint)
    }

    TypeArgumentList(first : ScriptType, rest : List[ScriptType]) : List[ScriptType]
    {
      MakeList(first, rest)
    }

    ScriptType(type : ScriptType, rank : List[NToken * NToken]) : ScriptType
    {
      if (rank.Count == 0) type else ScriptType.Array(type, rank.Count)
    }
    
    PredefinedType(type : NToken) : ScriptType
    {
      match(GetText(type))
      {
      | "any" => ScriptType.Any()
      | "number" => ScriptType.Number()
      | "boolean" | "bool" => ScriptType.Boolean()
      | "string" => ScriptType.String()
      | "void" => ScriptType.Void()
      | _ => assert(false);
      }
    }
    
    TypeName(first : string, rest : List[string]) : string
    {
      Id(first, rest)
    }
    
    ObjectType(o : ScriptType.Object) : ScriptType
    {
      o
    }
    
    ObjectTypeStrict(members : NToken) : ScriptType.Object
    {
      ScriptType.Object()
    }  
    
    ArrayType(type : ScriptType, rank : List[NToken * NToken]) : ScriptType.Array
    {
      ScriptType.Array(type, rank.Count)
    }
    /*
    TypeMemberListOpt(members : option[list[TSObject]]) : list[TSObject]
    {
      members ?? []
    }
    
    TypeMemberList(member : TSObject, rest : List[TSObject]) : list[TSObject]
    {
      member :: rest.NToList()
    }
    
    CallSignature(parms : List[TSParam], returnType : option[TSType]) : TSObject
    {
      TSObject.Call(parms, returnType)
    }

    ConstructSignature(parms : List[TSParam], type : option[TSType]) : TSObject
    {
      TSObject.Construct(parms, type)
    }
    
    IndexSignature(paramName : string, paramType : NToken, returnType : TSType) : TSObject
    {
      def indexParamType = 
        match(GetText(paramType))
        {
        | "number" => TSIndexParamType.Number()
        | "string" => TSIndexParamType.String()
        | _ => assert(false);
        };
      
      TSObject.Index(paramName, indexParamType, returnType)
    }
    
    PropertySignature(id : string, optional : bool, type : option[TSType]) : TSObject
    {
      TSObject.Property(id, optional, type)
    }

    FunctionSignature(id : string, optional : bool, parms : List[TSParam], returnType : option[TSType]) : TSObject
    {
      TSObject.Function(id, optional, parms, returnType)
    }
    */
    FunctionType(parms : NToken, returnType : ScriptType) : ScriptType
    {
      ScriptType.Function()
    }
    
    ConstructorType(parms : NToken, returnType : ScriptType) : ScriptType
    {
      ScriptType.Constructor()
    }
   
    IdentifierPath(first : string, rest : List[string]) : string
    {
      Id(first, rest)
    }
    /*
    Public(_ : NToken) : MemberAccess
    {
      MemberAccess.Public
    }
    
    Private(_ : NToken) : MemberAccess
    {
      MemberAccess.Private
    }
    
    ParameterListOpt(parms : option[list[TSParam]]) : List[TSParam]
    {
      parms ?? []
    }
    
    ParameterListROR(required : List[TSParam], optional : List[TSParam], rest : List[TSParam]) : List[TSParam]
    {
      required.Append(optional).Append(rest)
    }
    
    ParameterListOR(optional : List[TSParam], rest : List[TSParam]) : List[TSParam]
    {
      optional.Append(rest)
    }
    
    ParameterListRO(required : List[TSParam], optional : List[TSParam]) : List[TSParam]
    {
      required.Append(optional)
    }
    
    RequiredParameter(access : option[MemberAccess], id : string, type : option[TSType]) : TSParam
    {
      TSParam.Required(id, type, access)
    }
    
    ParameterListRR(required : List[TSParam], rest : List[TSParam]) : List[TSParam]
    {
      required.Append(rest)
    }
    
    // _optional is always true in declaration files
    OptionalParameter(access : option[MemberAccess], id : string, optional : bool, type : option[TSType]) : TSParam
    {
      TSParam.Option(id, optional, type, access)
    }
    
    RequiredParameterList(first : TSParam, rest : List[TSParam]) : List[TSParam]
    {
      first :: rest.NToList()
    }
    
    OptionalParameterList(first : TSParam, rest : List[TSParam]) : List[TSParam]
    {
      first :: rest.NToList()
    }
    
    RestParameter(id : string, type : TSType.Array) : List[TSParam]
    {
      [TSParam.Rest(id, type)]
    }
    */
    
    TypeQuery(query : string) : ScriptType
    {
      ScriptType.Query(query)
    }
    
    TypeQueryExpression(first : string, rest : List[string]) : string
    {
      Id(first, rest)
    }
    
    TypeReference(typeName : string, typeArguments : option[List[ScriptType]]) : ScriptType
    {
      ScriptType.Ref(typeName, typeArguments.WithDefault(List()))
    }
    
    InterfaceDeclaration(
      id : string,
      typeParameters : List[ScriptTypeParameter],
      extends : option[List[ScriptType]],
      type : ScriptType.Object) : DeclarationElement
    {
      DeclarationElement.InterfaceDeclaration(
        InterfaceDeclarationRule(id, typeParameters, extends.WithDefault(List()), type))
    }
    
    ClassOrInterfaceTypeList(first : ScriptType, rest : List[ScriptType]) : List[ScriptType]
    {
      MakeList(first, rest)
    }
    
    /*
    InterfaceNameList(first : string, rest : List[string]) : list[string]
    {
      first :: rest.NToList()
    }
    
    ClassHeritage(extends : option[string], implement : option[list[string]]) : ClassHeritage
    {
      (extends, implement ?? [])
    }
    */
    AmbientVariableDeclaration(name : string, type : option[ScriptType]) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.VariableDeclaration(name, type)
    }
    
    AmbientFunctionDeclaration(name : string, _signature : NToken) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.FunctionDeclaration(name)
    }
    
    AmbientClassDeclaration(
      name : string,
      typeParameters : List[ScriptTypeParameter],
      _ : NToken,
      _ : NToken) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.ClassDeclaration(name, typeParameters)
    }
    /*
    AmbientClassBody(elements : List[TSClassMember]) : list[TSClassMember]
    {
      elements.NToList()
    }
    
    AmbientConstructorDeclaration(parms : List[TSParam]) : TSClassMember
    {
      TSClassMember.Constructor(parms)
    }
    
    AmbientMemberVariable(id : string, type : option[TSType]) : TSClassMemberPart
    {
      TSClassMemberPart.Variable(id, type)
    }

    ClassMemberFunctionSignature(id : string, optional : bool, parms : List[TSParam], returnType : option[TSType]) : TSClassMemberPart
    {
      TSClassMemberPart.Method(id, optional, parms, returnType)
    }
    
    AmbientMemberDeclaration(access : option[MemberAccess], isStatic : option[bool], part : TSClassMemberPart) : TSClassMember
    {
      TSClassMember.Member(part, isStatic, access)
    }
    */
    
    AmbientExternalModuleDeclaration(name : string, _ : NToken) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.ExternalModuleDeclaration(name)
    }
    
    AmbientModuleDeclaration(name : string , elements : NToken) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.ModuleDeclaration(name)
    }
   /* 
    AmbientModuleBody(elements : List[TSGlobal]) : list[TSGlobal]
    {
      elements.NToList()
    }
    */
    AmbientEnumDeclaration(name : string, _ : NToken) : AmbientDeclarationRule
    {
      AmbientDeclarationRule.EnumDeclaration(name)
    }
    /*
    EnumBody(first : TSEnumElement, rest : List[TSEnumElement]) : list[TSEnumElement]
    {
      first :: rest.NToList()
    }
    
    EnumElement(id : string, initialiser : option[string]) : TSEnumElement
    {
      TSEnumElement(id, initialiser)
    }
    
    ExportAssignment(id : string) : TSGlobal
    {
      TSGlobal.Export(id)
    }
    */
    
    
    
    EntityName(first : string, rest : option[string]) : string
    {
      Id(first, rest)
    }
    
    AmbientDeclaration(value : AmbientDeclarationRule) : DeclarationElement
    {
      DeclarationElement.AmbientDeclaration(value)
    }
    
    ImportDeclaration(id : string, entity : string) : DeclarationElement
    {
      DeclarationElement.ImportDeclaration(ImportDeclarationRule(id, entity))
    }
    
    ExternalImportDeclaration(id : string, moduleReference : string) : DeclarationElement
    {
      DeclarationElement.ExternalImportDeclaration(ExternalImportDeclarationRule(id, moduleReference))
    }
    
    ExportModuleAssignment(id : string, moduleEntity : string) : ExportAssignmentRule
    {
      ExportAssignmentRule.Module(id, moduleEntity)
    }
    
    ExportIdentifierAssignment(id : string) : ExportAssignmentRule
    {
      ExportAssignmentRule.Identifier(id)
    }
    
    ExportAssignment(e : ExportAssignmentRule) : DeclarationElement
    {
      DeclarationElement.ExportAssignment(e)
    }    
    
    ReferencePath(_ : NToken, path : string) : ReferenceRule
    {
      ReferenceRule.Lib(path)
    }
    
    ReferenceNoDefaultLib(_ : NToken, _ : NToken) : ReferenceRule
    {
      ReferenceRule.NoDefaultLib()
    }
    
    ReferenceElement(_ : NToken, reference : ReferenceRule, _ : NToken) : DeclarationElement
    {
      DeclarationElement.Reference(reference)
    }
  }  
}
